import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Shapes
import Quickshell
import Quickshell.Io
import QtQuick.Effects
import qs.Commons
import qs.Ui

// Blip panel — threads, then an iMessage-style conversation with a compose box.
//
//   list         → every thread, newest first, unread marked
//   conversation → bubbles: mine blue on the right, theirs grey on the left,
//                  grouped by sender with one timestamp per run, day dividers,
//                  compose box pinned at the bottom. Esc goes back.
//
// Reads its thread list from the host widget so there is exactly one poller.
// Bubble decoration (grouping, day labels, times) is computed in thread.ts,
// where it is unit-tested; this file only renders.
FocusScope {
  id: root

  // ---- host contract (docs/app-design-review.md) ----------------------
  property var hostWidget: null
  property var preferences: null
  /** The surface hosting this view is showing (popout: opened; window:
   *  visible). Gates reads, reloads, and autofocus — a hidden surface must
   *  never mark anything read. */
  property bool surfaceOpen: false
  property color foreground: Color.foreground
  property color urgent: Color.urgent
  property string fontFamily: Style.font.family
  readonly property color dim: Qt.darker(foreground, 1.45)
  /** An editor owns the keyboard — the host's key catcher must stand down. */
  readonly property bool editorActive:
    settingsMode || composeField.activeFocus || searchField.activeFocus || newField.activeFocus || bubbleFocused
  readonly property alias composeEditor: composeField
  readonly property real contentHeightHint: listContent.implicitHeight
  /** The view wants keyboard navigation focus back (list mode). */
  signal navigationFocusRequested()
  // Palette follows the Omarchy theme (Fred, 2026-08-31). Color.* is the
  // live theme singleton — it hot-reloads on theme switch, so every accent,
  // bubble, and link repaints with the rest of the desktop. If the theme
  // ships no distinct accent (accent == foreground), fall back to iMessage
  // blue so "my" bubbles stay readable as mine.
  readonly property bool themeHasAccent: Color.accent.toString() !== Color.foreground.toString()
  readonly property color accent: themeHasAccent ? Color.accent : "#0a84ff"
  readonly property color cyan: accent            // legacy name; accents/links
  readonly property color okColor: accent

  // App-local scale tokens layered on the live Omarchy theme. Defaults are
  // exactly the pre-preferences values, so installing this layer is visual
  // no-op until the user changes preferences.json or the Settings UI.
  readonly property real fontScale: preferences ? preferences.fontScale : 1.0
  readonly property real density: preferences ? preferences.density : 1.0
  readonly property real cornerScale: preferences ? preferences.cornerScale : 1.0
  readonly property int avatarSize: preferences ? preferences.avatarSize : 30
  readonly property int groupMessageAvatarSize: Math.max(
    root.space(24), Math.round(Style.spaceReal(root.avatarSize) * 0.9)
  )
  readonly property int groupMessageAvatarSlot: groupMessageAvatarSize + root.space(8)
  readonly property string outgoingColorSetting: preferences ? preferences.outgoingBubbleColor : "theme"
  readonly property string incomingColorSetting: preferences ? preferences.incomingBubbleColor : "theme"
  function fontSize(value) { return Math.max(1, Math.round(value * fontScale)) }
  function space(value) { return Math.max(1, Math.round(Style.spaceReal(value) * density)) }
  function corner(value) { return Math.max(0, Math.round(value * cornerScale)) }
  function opaqueOver(fill, background) {
    var alpha = fill.a
    return Qt.rgba(
      fill.r * alpha + background.r * (1 - alpha),
      fill.g * alpha + background.g * (1 - alpha),
      fill.b * alpha + background.b * (1 - alpha), 1
    )
  }
  function contrastText(fill) {
    var alpha = fill.a
    var red = fill.r * alpha + Color.background.r * (1 - alpha)
    var green = fill.g * alpha + Color.background.g * (1 - alpha)
    var blue = fill.b * alpha + Color.background.b * (1 - alpha)
    return (0.299 * red + 0.587 * green + 0.114 * blue) > 0.35 ? "#1a1a1a" : "#ffffff"
  }
  function richMessageHtml(html, linkColor) {
    var color = String(linkColor || "").toLowerCase()
    if (!/^#[0-9a-f]{6}$/.test(color)) color = "#ffffff"
    return String(html || "").replace(
      /<a href=/g,
      '<a style="color: ' + color + '; text-decoration: underline;" href='
    )
  }

  readonly property color mineFill: outgoingColorSetting === "theme" ? accent : outgoingColorSetting
  // Bubble text picks black/white by which CONTRASTS better with the fill:
  // (L+0.05)/0.15 vs 1.05/(L+0.05) cross over near L≈0.35. A 0.6 threshold
  // chose white on medium accents where dark text is clearly more legible
  // (e.g. Evergreen #4a9a68: white 3.4:1 vs dark 5.1:1).
  readonly property color mineText: contrastText(mineFill)
  readonly property color theirsFill: incomingColorSetting === "theme"
    ? Qt.rgba(foreground.r, foreground.g, foreground.b, 0.14)
    : incomingColorSetting
  readonly property color theirsText: incomingColorSetting === "theme" ? foreground : contrastText(theirsFill)

  readonly property string home: Quickshell.env("HOME")
  readonly property string threadScript:
    decodeURIComponent(Qt.resolvedUrl("thread.ts").toString().replace(/^file:\/\//, ""))
  readonly property string fetchScript:
    decodeURIComponent(Qt.resolvedUrl("fetch.ts").toString().replace(/^file:\/\//, ""))
  readonly property string pasteScript:
    decodeURIComponent(Qt.resolvedUrl("paste.ts").toString().replace(/^file:\/\//, ""))
  readonly property string sendFileScript:
    decodeURIComponent(Qt.resolvedUrl("send-file.ts").toString().replace(/^file:\/\//, ""))
  readonly property string searchScript:
    decodeURIComponent(Qt.resolvedUrl("search.ts").toString().replace(/^file:\/\//, ""))

  readonly property string contactScript:
    decodeURIComponent(Qt.resolvedUrl("contact-search.ts").toString().replace(/^file:\/\//, ""))

  // ---- new-conversation state (`n` opens, Esc closes)
  property bool newMode: false
  property var newResults: []
  property string newNote: ""

  // ---- search state (list view only; `/` opens, Esc closes)
  property bool searching: false
  property var searchResults: []
  property string searchNote: ""
  // Results replace the thread list only once a search has actually produced
  // something to show — focusing the box alone must not blank the list.
  readonly property bool searchShowing:
    searching && (searchResults.length > 0 || searchNote !== "")

  // Fetched attachments: id → file:// url; "" = fetch failed (chip shows ⚠).
  // The cache under ~/.cache/blip/att is global, so results never go stale on
  // a thread switch — no ownership tracking needed, unlike thread loads.
  property var attFiles: ({})
  property var attMetrics: ({})
  property var fetchQueue: []
  property string fetchingId: ""
  // Compose draft attachment (one per message in v1).
  property string draftPath: ""
  property string draftLabel: ""
  // What the in-flight file send / paste belong to, so late completions
  // can't clobber a NEWER draft or land in a DIFFERENT conversation.
  property string sendDraftPath: ""
  property string pasteChat: ""

  readonly property var threads: hostWidget ? hostWidget.threads : []
  // Messages.app owns pin membership and order. Pinned conversations leave
  // the chronological list and render first as the familiar avatar grid.
  readonly property var pinnedThreads: {
    var out = []
    for (var i = 0; i < threads.length; i++) if (threads[i].pinned === true) out.push(threads[i])
    return out.sort(function(a, b) {
      var ao = Number(a.pin_order); var bo = Number(b.pin_order)
      if (ao !== bo) return ao - bo
      return String(a.last_ts) < String(b.last_ts) ? 1 : -1
    })
  }
  readonly property var regularThreads: threads.filter(function(thread) { return thread.pinned !== true })
  readonly property var navigationThreads: pinnedThreads.concat(regularThreads)
  readonly property bool online: hostWidget ? hostWidget.online : false
  readonly property int unread: hostWidget ? hostWidget.unread : 0

  // ---- view state
  property var active: null          // selected thread object, null = list view
  property var bubbles: []           // decorated messages for `active` (see thread.ts)
  property bool loading: false
  property string note: ""           // transient status line (send result, errors)
  property int cursor: -1            // keyboard row selection in list view
  property bool pinToBottom: false   // scroll to the newest bubble once layout settles
  property bool bubbleFocused: false // a bubble's TextEdit has focus (text selection in progress)
  property string threadRunningChat: "" // chat owned by the current threadProc
  property string bubblesJson: ""       // last rendered bubbles, for no-op reload detection
  property bool firstLoad: true         // first load of the open thread pins to bottom
  property bool settingsMode: false
  property string pendingThreadChat: "" // latest chat requested while it runs
  property var pendingThreadAliases: [] // historical ids coalesced into that chat
  property var threadRunningAliases: []
  property string sendChat: ""          // immutable context for the current send
  property string sendText: ""
  property string reloadChat: ""
  property string contactToast: ""
  property bool contactToastError: false
  property var contextPeople: []
  property string contextMessageText: ""

  readonly property bool inThread: active !== null
  // last_ts of the open conversation as of its last load — the push watcher
  // refreshes the thread list, and when OUR thread advances, the bubbles
  // reload themselves. The guard makes unchanged refreshes free.
  property string activeLastTs: ""
  onThreadsChanged: {
    // LIST view: a rebuilt Repeater collapses contentHeight for a frame and
    // StopAtBounds clamps contentY to 0 — the reader's place in a long list
    // was lost on every genuine refresh. Restore it once layout settles.
    if (!inThread && surfaceOpen) {
      var y = threadFlick.contentY
      if (y > 0) Qt.callLater(function() {
        if (!root.inThread)
          threadFlick.contentY = Math.max(0, Math.min(y, threadFlick.contentHeight - threadFlick.height))
      })
      return
    }
    // `opened` too, not just inThread: a CLOSED panel still holds `active`,
    // and reloading a hidden thread marks it read without it ever being
    // seen (Codex HIGH, 1.1.0 review).
    if (!inThread || !surfaceOpen) return
    for (var i = 0; i < threads.length; i++) {
      var t = threads[i]
      if (String(t.chat) !== String(active.chat)) continue
      if (String(t.last_ts) !== activeLastTs) {
        activeLastTs = String(t.last_ts)
        active = t              // fresher name/guid too (a group can BECOME sendable)
        // The push ping usually started this exact load already — don't
        // stack a second one behind it.
        var busyHere = (threadProc.running && threadRunningChat === String(t.chat))
                       || pendingThreadChat === String(t.chat)
        if (!busyHere) {
          if (!flick.stick) { pushPending = true }  // reading history — defer
          else {
            pinToBottom = true
            requestThreadLoad(String(t.chat), t.aliases || [])
          }
        }
      }
      return
    }
  }
  // Same rule as collector.isGroupChat(): anything that is not a phone/email.
  function isGroupId(c) { c = String(c || ""); return c !== "" && !/^\+?[0-9]{5,}$/.test(c) && c.indexOf("@") < 0 }
  function isContactHandle(value) {
    var handle = String(value || "")
    return /^\+?[0-9][0-9 ()./-]{2,39}$/.test(handle)
      || /^[^@\s]+@[^@\s]+$/.test(handle)
  }
  function contactHandleKey(value) {
    var handle = String(value || "")
    if (handle.indexOf("@") >= 0) return "email:" + handle.toLowerCase()
    var digits = handle.replace(/\D/g, "")
    return "phone:" + (digits.length >= 10 ? digits.slice(-10) : digits)
  }
  function contactPeople(thread) {
    if (!thread) return []
    var result = []
    var seen = ({})
    function add(handle, name) {
      handle = String(handle || "").trim()
      if (!root.isContactHandle(handle)) return
      var key = root.contactHandleKey(handle)
      if (seen[key] || result.length >= 64) return
      seen[key] = true
      name = String(name || handle).trim()
      result.push({ handle: handle, name: name === "" ? handle : name })
    }
    if (!root.isGroupId(String(thread.chat || ""))) {
      add(thread.handle || thread.chat, thread.pin_name || thread.name || thread.chat)
      return result
    }
    var participants = Array.isArray(thread.participants) ? thread.participants : []
    for (var i = 0; i < participants.length; i++) {
      var person = participants[i]
      if (typeof person === "string") add(person, person)
      else add(person && person.handle, person && person.name)
    }
    // Cached group metadata from an older build may not have participant
    // names yet. Recent inbound bubbles safely fill that gap until refresh.
    if (thread === root.active) {
      for (var j = 0; j < root.bubbles.length; j++) {
        var bubble = root.bubbles[j]
        if (!bubble.from_me) add(bubble.handle, bubble.name)
      }
    }
    return result
  }
  function personMenuLabel(person) {
    var name = String(person && person.name || "")
    var handle = String(person && person.handle || "")
    return name !== "" && name !== handle ? name + "  ·  " + handle : handle
  }
  function showContactToast(message, failed) {
    contactToast = String(message || "")
    contactToastError = failed === true
    contactToastTimer.restart()
  }
  function openContactContext(thread, messageText) {
    var people = contactPeople(thread)
    contextPeople = people
    contextMessageText = String(messageText || "")
    if (people.length === 0 && contextMessageText === "") {
      showContactToast("No contact person is available for this conversation", true)
      return
    }
    if (people.length === 0) messageOnlyMenu.popup()
    else if (people.length === 1)
      (contextMessageText === "" ? directContactMenu : directMessageMenu).popup()
    else
      (contextMessageText === "" ? groupContactMenu : groupMessageMenu).popup()
  }
  function copyContactVCard(person) {
    if (!person || !isContactHandle(person.handle)) return
    if (!settingsView.copyVCard(person.handle)) {
      showContactToast("Contacts is busy; try again in a moment", true)
      return
    }
    showContactToast("Exporting “" + person.name + "” from Mac Contacts…", false)
  }
  function editContact(person) {
    if (!person || !isContactHandle(person.handle)) return
    openSettings("contacts")
    Qt.callLater(function() { settingsView.editContact(person.handle) })
  }
  function threadContainsChat(thread, chat) {
    if (!thread) return false
    var wanted = String(chat || "")
    if (String(thread.chat || "") === wanted) return true
    var aliases = Array.isArray(thread.aliases) ? thread.aliases : []
    for (var i = 0; i < aliases.length; i++)
      if (String(aliases[i]) === wanted) return true
    return false
  }
  readonly property bool activeIsGroup: inThread && isGroupId(active.chat)

  /**
   * DMs send --to the chat id (a phone/email). Groups send --chat-id with the
   * full AppleScript GUID ("any;+;<id>") that `imsg groups` supplies; a group
   * whose GUID is not cached yet stays read-only rather than guess. Never the
   * handle: a group's handle is whichever member spoke last, and sending to it
   * would DM that one person while the panel shows the group.
   */
  function isSendable(t) {
    if (!t) return false
    var c = String(t.chat || "")
    if (isGroupId(c)) return /^[A-Za-z]+;[+-];.+$/.test(String(t.guid || ""))
    return /^\+?[0-9]{5,}$/.test(c) || c.indexOf("@") > 0
  }

  /** Back to the list view, scrolled to top — the host calls this on open. */
  function resetToList() {
    settingsMode = false
    active = null
    bubbles = []
    note = ""
    cursor = -1
    loading = false
    pendingThreadChat = ""
    pendingThreadAliases = []
    composeField.text = ""
    searching = false
    searchResults = []
    searchNote = ""
    searchField.text = ""
    newMode = false
    newResults = []
    newNote = ""
    newField.text = ""
    // Deep fetch for a real thread list. Does NOT clear dots: like iMessage,
    // a thread stays marked until that conversation is opened.
    if (hostWidget) hostWidget.refresh(true, false)
    Qt.callLater(function() { threadFlick.contentY = 0 })
  }

  function back() {
    active = null
    bubbles = []
    note = ""
    loading = false
    pendingThreadChat = ""
    pendingThreadAliases = []
    composeField.text = ""
    clearDraft()   // a queued file must never survive into another thread
    pinToBottom = false
    Qt.callLater(function() { threadFlick.contentY = 0; root.navigationFocusRequested() })
  }

  function openSettings(page) {
    exitSearch()
    exitNew()
    settingsMode = true
    settingsView.showPage(String(page || "contacts") === "appearance" ? "appearance" : "contacts")
    Qt.callLater(settingsView.focusDefault)
  }

  function closeSettings() {
    settingsMode = false
    Qt.callLater(root.focusDefault)
  }

  function openThread(t) {
    if (!t) return
    active = t
    activeLastTs = String(t.last_ts || "")
    bubbles = []
    bubblesJson = ""
    firstLoad = true
    pushPending = false
    note = ""
    loading = true
    composeField.text = ""
    clearDraft()   // a queued file must never survive into another thread
    requestThreadLoad(String(t.chat), t.aliases || [])
    Qt.callLater(function() { composeField.forceActiveFocus() })
  }

  function requestThreadLoad(chat, aliases) {
    pendingThreadChat = String(chat || "")
    pendingThreadAliases = Array.isArray(aliases) ? aliases.slice(0, 16) : []
    if (!threadProc.running) startNextThreadLoad()
  }

  function startNextThreadLoad() {
    if (threadProc.running || pendingThreadChat === "") return
    threadRunningChat = pendingThreadChat
    threadRunningAliases = pendingThreadAliases
    pendingThreadChat = ""
    pendingThreadAliases = []
    threadProc.command = ["bun", root.threadScript, threadRunningChat, "80"].concat(threadRunningAliases)
    threadProc.running = true
  }

  /** IPC test hook: drive the exact user send path minus the keyboard.
   *  Keystroke injection (wtype) proved non-deterministic — a virtual
   *  keyboard's events can land on whatever surface Hyprland favors. */
  /** Clear every badge/dot locally. Read state never goes back to iMessage. */
  function markAllRead() {
    if (!root.hostWidget || root.unread === 0) return
    root.hostWidget.markAllRead()
  }

  /** Chip icon for an attachment's mime type. */
  function attachmentIcon(mime) {
    var m = String(mime || "")
    if (m.indexOf("image/") === 0) return "📷"
    if (m.indexOf("video/") === 0) return "🎬"
    if (m.indexOf("audio/") === 0) return "🎤"
    if (m === "application/pdf") return "📄"
    return "📎"
  }

  /** "❤️👍" — the emoji string for a bubble's tapback pill. */
  function tapbackRow(list) {
    var s = ""
    for (var i = 0; i < (list || []).length; i++) s += list[i].emoji
    return s
  }

  // ---------------------------------------------------- attachment fetching

  function isImageMime(m) { return String(m || "").indexOf("image/") === 0 }
  function linkHost(u) { var m = /^https?:\/\/([^/?#]+)/i.exec(String(u || "")); return (m ? m[1] : String(u || "")).replace(/^www\./, "").toLowerCase() }
  /** Only http(s) ever reaches xdg-open from a card (thread.ts filters too). */
  function openLink(u) {
    u = String(u || "")
    if (!/^https?:\/\//i.test(u)) return
    openProc.command = ["xdg-open", u]
    openProc.running = true
  }

  // ---------------------------------------------------- contact photos
  // Sidebar avatars: `imsg avatar <handle>` via avatar.ts (7-day cache, negative
  // markers). One request in flight; rows ask on creation; groups never ask.
  readonly property string avatarScript: fetchScript.replace(/fetch\.ts$/, "avatar.ts")
  property var avatarFiles: ({})     // handle → file:// url, "" = no photo
  property var avatarQueue: []
  function requestAvatar(handle) {
    handle = String(handle || "")
    if (handle === "" || isGroupId(handle)) return
    if (avatarFiles[handle] !== undefined || avatarQueue.indexOf(handle) >= 0) return
    avatarQueue.push(handle)
    pumpAvatar()
  }
  function pumpAvatar() {
    if (avatarProc.running || avatarQueue.length === 0) return
    avatarProc.handle = avatarQueue.shift()
    avatarProc.command = ["bun", root.avatarScript, avatarProc.handle]
    avatarProc.running = true
  }
  Process {
    id: avatarProc
    property string handle: ""
    stdout: StdioCollector {
      onStreamFinished: {
        var url = ""
        try { var d = JSON.parse(text.trim()); if (d.ok === true) url = String(d.url || "") } catch (e) {}
        var m = Object.assign({}, root.avatarFiles)
        m[avatarProc.handle] = url
        root.avatarFiles = m
        root.pumpAvatar()
      }
    }
    onExited: Qt.callLater(root.pumpAvatar)
  }
  /** Only media/documents are handed to xdg-open. Anything a sender could
   *  make executable (scripts, .desktop, unknown blobs) is saved and named,
   *  never launched (Codex audit #10). */
  function openableMime(m) {
    m = String(m || "")
    return m.indexOf("image/") === 0 || m.indexOf("video/") === 0 || m.indexOf("audio/") === 0 ||
           m === "application/pdf" || m === "text/plain" || m === "text/vcard" || m === "text/calendar"
  }

  function enqueueFetch(att, openWhenDone, auto) {
    var id = String(att.id || "")
    if (id === "" || fetchingId === id) return
    if (attFiles[id] !== undefined && !openWhenDone) return
    for (var i = 0; i < fetchQueue.length; i++) {
      if (fetchQueue[i].id === id) {
        if (openWhenDone) fetchQueue[i].open = true
        return
      }
    }
    fetchQueue.push({ id: id, name: String(att.name || "file"),
                      mime: String(att.mime || ""), open: openWhenDone === true,
                      auto: auto === true })
    pumpFetch()
  }

  property bool fetchJobOpen: false
  property string fetchJobMime: ""
  function pumpFetch() {
    if (fetchProc.running || fetchQueue.length === 0) return
    var job = fetchQueue.shift()
    fetchingId = job.id
    fetchJobOpen = job.open === true
    fetchJobMime = job.mime
    // Auto-pulls carry a hard transfer cap: claimed metadata is not the limit.
    fetchProc.command = ["bun", root.fetchScript, job.id, job.name, job.mime, job.auto ? "5242880" : ""]
    fetchProc.running = true
  }

  /** Images ≤ 5 MB in the open conversation fetch themselves (Fred's call —
   *  it's what makes the panel feel like Messages, and cached hits are free). */
  function autoFetchImages() {
    if (!inThread) return
    for (var i = 0; i < bubbles.length; i++) {
      var atts = bubbles[i].attachments || []
      for (var j = 0; j < atts.length; j++) {
        // bytes must be KNOWN and small — null/0 must not slip under the cap
        // and auto-pull something the click-path 100 MB ceiling would allow.
        var b = atts[j].bytes
        if (isImageMime(atts[j].mime) && typeof b === "number" && b > 0 && b <= 5 * 1024 * 1024)
          enqueueFetch(atts[j], false, true)
      }
      // link-card preview PNGs are small; the auto-fetch transfer cap bounds them
      var l = bubbles[i].link
      if (l && l.image_id) enqueueFetch({ id: String(l.image_id), name: "preview.png", mime: "image/png", bytes: 0 }, false, true)
    }
  }

  /** Chip/image click: fetch-then-open. ALWAYS round-trips fetch.ts, even
   *  when attFiles already has a url — the LRU may have evicted the file
   *  since (Codex finding #5); a cache hit is instant anyway. */
  function openAttachment(att) {
    var id = String(att.id || "")
    if (id === "") return
    if (attFiles[id] === "") {   // failed marker — clear it so a retry runs
      var m = Object.assign({}, attFiles); delete m[id]; attFiles = m
    }
    enqueueFetch(att, true)
  }

  // ------------------------------------------------------ compose attachment

  function setDraft(path) {
    var p = String(path || "")
    if (p === "") return
    draftPath = p
    var parts = p.split("/")
    draftLabel = parts[parts.length - 1] || "file"
    Qt.callLater(function() { composeField.forceActiveFocus() })
  }

  function clearDraft() {
    draftPath = ""
    draftLabel = ""
  }

  function startPaste() {
    if (pasteProc.running || !inThread) return
    pasteChat = String(active.chat)
    pasteProc.command = ["bun", root.pasteScript]
    pasteProc.running = true
  }

  // ------------------------------------------------- new conversation

  function startNew() {
    if (inThread && !splitView) return   // split view: the list pane is right there
    exitSearch()
    newMode = true
    newResults = []
    newNote = ""
    // Defer focus until the visibility change has completed its layout pass.
    Qt.callLater(function() { newField.forceActiveFocus(); newField.selectAll() })
  }

  function exitNew() {
    newMode = false
    newResults = []
    newNote = ""
    newField.text = ""
    Qt.callLater(root.navigationFocusRequested)
  }

  // Same identity discipline as message search: a stale completion must
  // never surface Alice's results under Bob's query (Codex HIGH, 1.2.0).
  property int contactSeq: 0
  property string contactPending: ""
  function runContactSearch() {
    var q = newField.text.trim()
    if (q === "") return
    // Bump the generation NOW so the in-flight (older) result is rejected,
    // never shown as clickable rows under the newer query (Codex audit #5).
    if (contactProc.running) { contactSeq++; newResults = []; newNote = "searching contacts…"; contactPending = q; return }
    contactSeq++
    newNote = "searching contacts…"
    newResults = []
    contactProc.command = ["bun", root.contactScript, q]
    contactProc.running = true
  }

  /** Start (or resume) a DM with a picked handle. An existing thread is
   *  reused so history shows; otherwise a synthetic DM thread — sendable,
   *  because DMs only need --to <handle>. */
  function openContact(hit) {
    newMode = false
    for (var i = 0; i < threads.length; i++) {
      if (String(threads[i].chat) === String(hit.handle)) { openThread(threads[i]); return }
    }
    openThread({ chat: hit.handle, guid: "", name: hit.name, handle: hit.handle,
                 service: "iMessage", last_ts: "", last_text: "",
                 last_from_me: false, count: 0, unread: 0 })
  }

  // ------------------------------------------------------------- search

  function startSearch() {
    if (inThread && !splitView) return
    searching = true
    searchResults = []
    searchNote = ""
    Qt.callLater(function() { searchField.forceActiveFocus(); searchField.selectAll() })
  }

  function exitSearch() {
    searching = false
    searchResults = []
    searchNote = ""
    searchField.text = ""
    Qt.callLater(function() { root.navigationFocusRequested() })
  }

  // Monotonic id so a stale completion can never label itself with a newer
  // query; a query typed while one runs is queued (latest wins) and fires
  // when the runner frees up — same pattern as thread loads.
  property int searchSeq: 0
  property string searchPending: ""
  function runSearch() {
    var q = searchField.text.trim()
    if (q === "") return
    if (searchProc.running) { searchSeq++; searchResults = []; searchNote = "searching…"; searchPending = q; return }
    searchSeq++
    searchNote = "searching…"
    searchResults = []
    searchProc.command = ["bun", root.searchScript, q, "40"]
    searchProc.running = true
  }

  /** Push ping while this conversation is open: reload its bubbles now,
   *  without waiting for the collector round-trip. Cheap when nothing
   *  changed; the thread loader already serializes concurrent requests. */
  // A reload REBUILDS every bubble and (when pinned) yanks the view to the
  // bottom — doing that on every push ping made reading history impossible
  // (Fred: "FIX SCROLLING!"). While the user is scrolled up, defer; the
  // reload fires the moment they return to the bottom.
  property bool pushPending: false
  function pushReload() {
    if (!inThread || !surfaceOpen) return
    if (!flick.stick) { pushPending = true; return }
    if (threadProc.running && pendingThreadChat !== "") return
    pinToBottom = true
    requestThreadLoad(String(active.chat), active.aliases || [])
  }

  /** IPC hook (`newchat <query>`): drive the composer path minus the keyboard. */
  function newChatFor(query) {
    if (inThread) back()
    startNew()
    newField.text = String(query || "")
    runContactSearch()
    return "contact search: " + newField.text
  }

  /** IPC hook (`find <query>`): drive the exact search path minus the keyboard. */
  function searchFor(query) {
    if (inThread) back()
    startSearch()
    searchField.text = String(query || "")
    runSearch()
    return "searching: " + searchField.text
  }

  /** Open the conversation a search hit belongs to. Prefer the live thread
   *  object (sendable, has the group guid); an old conversation outside the
   *  poll window opens read-mostly from the hit's identity. */
  function openSearchHit(hit) {
    searching = false
    for (var i = 0; i < threads.length; i++) {
      if (threadContainsChat(threads[i], hit.chat)) { openThread(threads[i]); return }
    }
    openThread({ chat: hit.chat, guid: "", name: hit.name, handle: hit.handle,
                 service: hit.service, last_ts: hit.ts, last_text: "",
                 last_from_me: false, count: 0, unread: 0 })
  }

  function composeAndSend(text) {
    if (!inThread) return "not in a thread"
    composeField.text = String(text || "")
    send()
    return note === "" ? "sent-dispatch" : note
  }

  /** Model truth for the bubble view, for automated verification. */
  function bubbleModel() {
    return JSON.stringify(bubbles.map(function(b) {
      return { mine: b.from_me === true, text: String(b.text || "").substring(0, 30) }
    }))
  }

  function send() {
    var text = composeField.text
    if (!root.inThread) return

    // "/attach <path>" queues a file from anywhere on this machine as the draft.
    var trimmed = text.trim()
    if (trimmed.indexOf("/attach ") === 0) {
      var p = trimmed.slice(8).trim()
      if (p.indexOf("~/") === 0) p = root.home + p.slice(1)
      setDraft(p)
      composeField.text = ""
      note = "attached — type a caption or press Enter to send"
      return
    }

    if (draftPath === "" && trimmed === "") return
    if (sendProc.running || fileSendProc.running) {
      note = "a message is already sending"
      return
    }
    if (!isSendable(root.active)) {
      note = "Read-only — group id unknown — send from your phone"
      return
    }
    note = "sending…"
    sendChat = String(root.active.chat)
    sendText = text

    if (draftPath !== "") {
      // send-file.ts owns target resolution (group guid or DM handle).
      sendDraftPath = draftPath
      fileSendProc.command = ["bun", root.sendFileScript, sendChat, draftPath]
        .concat(trimmed !== "" ? [text] : [])
      fileSendProc.running = true
      return
    }

    // Body on STDIN (--text-stdin), never argv: argv is readable by every
    // process on this machine and travels through ssh into the Mac's ps.
    var target = root.activeIsGroup
      ? ["--chat-id", String(root.active.guid)]
      : ["--to", sendChat]
    sendProc.command = [root.home + "/bin/imsg-send"].concat(target).concat(["--yes", "--text-stdin"])
    sendProc.stdinEnabled = true
    sendProc.running = true
    sendProc.write(text)
    sendProc.stdinEnabled = false
  }

  Process { id: copyProc }
  function copyText(t) {
    if (t === "") return
    // stdin, not argv: message text can be long and can start with "-".
    copyProc.command = ["sh", "-c", "wl-copy"]
    copyProc.stdinEnabled = true
    copyProc.running = true
    copyProc.write(t)
    copyProc.stdinEnabled = false
    note = "copied"
    noteTimer.restart()
  }
  Timer { id: noteTimer; interval: 1500; onTriggered: if (root.note === "copied") root.note = "" }

  function fmtTime(ts) {
    var s = String(ts || "")
    if (s.length < 16) return s
    var hour = Number(s.substring(11, 13))
    var minute = s.substring(14, 16)
    var use12 = !root.preferences || root.preferences.use12HourConversationTimes
    var clock = ""
    if (use12) {
      var suffix = hour >= 12 ? "PM" : "AM"
      var displayHour = hour % 12
      if (displayHour === 0) displayHour = 12
      clock = displayHour + ":" + minute + " " + suffix
    } else {
      clock = s.substring(11, 16)
    }
    var today = Qt.formatDateTime(new Date(), "yyyy-MM-dd")
    if (s.substring(0, 10) === today) return clock
    var month = Number(s.substring(5, 7))
    var day = Number(s.substring(8, 10))
    return month + "/" + day + " " + clock
  }

  // ------------------------------------------------------------ processes
  Process {
    id: threadProc
    stdout: StdioCollector {
      onStreamFinished: {
        // `opened` matters: a load finishing after the panel closed must not
        // render into a hidden view or mark the thread read unseen.
        var belongsHere = root.surfaceOpen && root.inThread && String(root.active.chat) === root.threadRunningChat
        if (!belongsHere) return
        root.loading = false
        try {
          var d = JSON.parse(text.trim())
          if (d.ok === true) {
            var list = Array.isArray(d.bubbles) ? d.bubbles : []
            var j = JSON.stringify(list)
            if (j === root.bubblesJson) {
              // Nothing changed — do NOT rebuild the Repeater (a rebuild
              // resets scroll and re-decodes every image). Push pings mostly
              // produce identical content; this makes them free.
              if (root.hostWidget)
                root.hostWidget.markThreadRead(root.threadRunningChat, root.threadRunningAliases)
              return
            }
            root.bubblesJson = j
            root.bubbles = list
            // Pin only on the thread's FIRST load or when the user was
            // already at the bottom — never while they read history.
            root.pinToBottom = root.firstLoad || flick.stick
            root.firstLoad = false
            Qt.callLater(root.autoFetchImages)
            // A dot means "looked at", so clear it only after content loaded.
            if (root.hostWidget)
              root.hostWidget.markThreadRead(root.threadRunningChat, root.threadRunningAliases)
          } else {
            root.bubbles = []
            root.note = String(d.error || "could not load this thread")
          }
        } catch (e) {
          root.bubbles = []
          root.note = "could not load this thread"
        }
      }
    }
    onExited: function(code, status) {
      var completedChat = root.threadRunningChat
      var belongsHere = root.inThread && String(root.active.chat) === completedChat
      root.threadRunningChat = ""
      root.threadRunningAliases = []
      if (belongsHere && root.pendingThreadChat === "") root.loading = false
      if (belongsHere && code !== 0) root.note = "thread loader failed (exit " + code + ")"
      if (root.pendingThreadChat !== "") Qt.callLater(root.startNextThreadLoad)
    }
  }

  Process {
    id: sendProc
    onExited: function(code, status) {
      var completedChat = root.sendChat
      var completedText = root.sendText
      var belongsHere = root.inThread && String(root.active.chat) === completedChat
      root.sendChat = ""
      root.sendText = ""
      if (code === 0) {
        if (belongsHere) {
          root.note = ""
          // Never erase a newer draft typed after this send began.
          if (composeField.text === completedText) composeField.text = ""
        }
        // Give Messages.app a beat to write the row, then reload the thread.
        root.reloadChat = completedChat
        reloadTimer.restart()
      } else if (belongsHere) {
        if (code === 69 || code === 255) root.note = "not sent — Mac unreachable"
        else root.note = "send failed (exit " + code + ")"
      }
      if (belongsHere) composeField.forceActiveFocus()
    }
  }

  // Attachment fetcher: one job at a time off root.fetchQueue.
  Process {
    id: fetchProc
    stdout: StdioCollector {
      onStreamFinished: {
        var id = root.fetchingId
        try {
          var d = JSON.parse(text.trim())
          var m = Object.assign({}, root.attFiles)
          m[id] = d.ok === true ? String(d.url || "") : ""
          root.attFiles = m
          var ratio = Number(d.pixelRatio)
          var pixelWidth = Number(d.pixelWidth)
          var pixelHeight = Number(d.pixelHeight)
          if (!isFinite(ratio) || ratio < 1 || ratio > 4) ratio = 1
          if (!isFinite(pixelWidth) || pixelWidth < 1 || pixelWidth > 100000) pixelWidth = 0
          if (!isFinite(pixelHeight) || pixelHeight < 1 || pixelHeight > 100000) pixelHeight = 0
          var metrics = Object.assign({}, root.attMetrics)
          metrics[id] = { pixelRatio: ratio, pixelWidth: pixelWidth, pixelHeight: pixelHeight }
          root.attMetrics = metrics
          if (d.ok === true && root.fetchJobOpen) {
            if (root.openableMime(root.fetchJobMime)) {
              openProc.command = ["xdg-open", String(d.url || "")]
              openProc.running = true
            } else {
              root.note = "saved, not opened (" + (root.fetchJobMime || "unknown type") + "): " + String(d.path || "")
            }
          }
          if (d.ok !== true && d.online === false) root.note = "fetch failed — Mac unreachable"
        } catch (e) {
          var m2 = Object.assign({}, root.attFiles)
          m2[id] = ""
          root.attFiles = m2
        }
      }
    }
    onExited: function(code, status) {
      root.fetchingId = ""
      Qt.callLater(root.pumpFetch)
    }
  }

  // Clipboard snapshot (Ctrl+V): file/image → draft chip, text → composer.
  Process {
    id: pasteProc
    stdout: StdioCollector {
      onStreamFinished: {
        // A slow clipboard read must never attach content to a conversation
        // the user has since navigated away from (Codex finding #2).
        if (!root.inThread || String(root.active.chat) !== root.pasteChat) return
        try {
          var d = JSON.parse(text.trim())
          if (d.kind === "file" || d.kind === "image") root.setDraft(String(d.path || ""))
          else if (d.kind === "text") {
            composeField.insert(composeField.cursorPosition, String(d.text || ""))
          }
        } catch (e) { /* clipboard empty or helper failed — nothing to paste */ }
      }
    }
  }

  // File send via send-file.ts (target resolution + stdin transfer live there).
  Process {
    id: fileSendProc
    stdout: StdioCollector {
      onStreamFinished: {
        var belongsHere = root.inThread && String(root.active.chat) === root.sendChat
        var ok = false, err = "file send failed"
        try {
          var d = JSON.parse(text.trim())
          ok = d.ok === true
          if (!ok) err = d.online === false ? "not sent — Mac unreachable" : String(d.error || err)
        } catch (e) { /* fall through */ }
        if (ok) {
          // Only clear the draft this send actually shipped — the user may
          // have queued a NEWER file while this one was in flight.
          if (root.draftPath === root.sendDraftPath) root.clearDraft()
          if (belongsHere) {
            root.note = ""
            if (composeField.text === root.sendText) composeField.text = ""
          }
          root.reloadChat = root.sendChat
          reloadTimer.restart()
        } else if (belongsHere) {
          root.note = err
        }
        root.sendChat = ""
        root.sendText = ""
        if (belongsHere) composeField.forceActiveFocus()
      }
    }
  }

  Process { id: openProc }

  Process {
    id: contactProc
    property int seq: 0
    onStarted: seq = root.contactSeq
    stdout: StdioCollector {
      onStreamFinished: {
        if (!root.newMode || contactProc.seq !== root.contactSeq) return // Esc'd or superseded
        try {
          var d = JSON.parse(text.trim())
          if (d.ok === true) {
            root.newResults = Array.isArray(d.results) ? d.results : []
            root.newNote = root.newResults.length === 0 ? "no matches — try a number or email" : ""
          } else {
            root.newNote = String(d.error || "contact search failed")
          }
        } catch (e) {
          root.newNote = "contact search failed"
        }
      }
    }
    onExited: if (root.contactPending !== "") {
      root.contactPending = ""
      if (root.newMode) Qt.callLater(root.runContactSearch)
    }
  }

  Process {
    id: searchProc
    property int seq: 0
    onStarted: seq = root.searchSeq
    stdout: StdioCollector {
      onStreamFinished: {
        if (!root.searching || searchProc.seq !== root.searchSeq) return // Esc'd or superseded
        try {
          var d = JSON.parse(text.trim())
          if (d.ok === true) {
            root.searchResults = Array.isArray(d.results) ? d.results : []
            root.searchNote = root.searchResults.length === 0 ? "no matches" : ""
          } else {
            root.searchNote = String(d.error || "search failed")
          }
        } catch (e) {
          root.searchNote = "search failed"
        }
      }
    }
    // A query queued mid-run fires now; runSearch reads the LIVE field, so
    // anything typed since supersedes the queued text automatically.
    onExited: if (root.searchPending !== "") {
      root.searchPending = ""
      if (root.searching) Qt.callLater(root.runSearch)
    }
  }

  Timer {
    id: reloadTimer
    interval: 1500
    onTriggered: if (root.inThread && String(root.active.chat) === root.reloadChat) {
      root.loading = true
      root.requestThreadLoad(root.reloadChat, root.active ? root.active.aliases || [] : [])
    }
  }

  // ---- keyboard navigation (the host's PanelKeyCatcher calls these)
  function moveCursor(dy) {
    if (settingsMode || inThread || navigationThreads.length === 0 || dy === 0) return
    cursor = (cursor + dy + navigationThreads.length) % navigationThreads.length
  }
  function activateCursor() {
    if (!inThread && cursor >= 0) openThread(navigationThreads[cursor])
  }
  function handleTextKey(text) {
    if (settingsMode || inThread) return
    if (text === "/") startSearch()
    else if (text === "n" || text === "N") startNew()
    else if (searching || newMode) return
    else if (text === "r" || text === "R") { if (hostWidget) hostWidget.refresh(true, false) }
    else if (text === "a" || text === "A") markAllRead()
    else if (text >= "1" && text <= "9") {
      var i = Number(text) - 1
      if (i < navigationThreads.length) openThread(navigationThreads[i])
    }
  }
  /** Esc semantics for a host without a PanelKeyCatcher (the window): true if
   *  something was unwound, false if the host should close. */
  function unwind() {
    if (settingsMode) { closeSettings(); return true }
    if (inThread) { back(); return true }
    if (newMode) { exitNew(); return true }
    if (searching) { exitSearch(); return true }
    return false
  }
  function focusDefault() {
    if (settingsMode) settingsView.focusDefault()
    else if (inThread) composeField.forceActiveFocus()
    else navigationFocusRequested()
  }

  // ---- layout: one pane (popout) or two (window). Both panes always exist —
  // hiding, not unloading, keeps image state, selection, and scroll position.
  property bool splitView: false
  property int sidebarWidth: preferences ? preferences.sidebarWidth : 320
  readonly property bool listShowing: splitView || !inThread

  RowLayout {
    anchors.fill: parent
    spacing: 0
    visible: !root.settingsMode

    // ------------------------------------------------------- thread pane
    Item {
      id: threadPane
      visible: root.listShowing
      Layout.fillHeight: true
      Layout.fillWidth: !root.splitView
      Layout.preferredWidth: root.splitView ? root.sidebarWidth : -1
      ColumnLayout {
        anchors.fill: parent
        // BlipWindow already supplies the outer edge inset. Keep only a small
        // inner gutter here so the sidebar does not pay for the same padding
        // twice; retain the larger right gutter beside the pane divider.
        anchors.leftMargin: root.splitView ? root.space(6) : 0
        anchors.rightMargin: root.splitView ? root.space(18) : 0
        anchors.topMargin: root.splitView ? root.space(10) : 0
        anchors.bottomMargin: root.splitView ? root.space(10) : 0
        spacing: root.space(root.splitView ? 14 : 8)
        RowLayout {
          Layout.fillWidth: true
          spacing: root.space(8)
          PanelHero {
            Layout.fillWidth: true
            title: "Blip"
            meta: (!root.online
                  ? "Mac unreachable — bridge offline"
                  : (root.unread > 0 ? root.unread + " unread" : "all caught up"))
            detail: ""   // Fred: not needed — and it squeezed the title to "B…"
            foreground: root.foreground
            fontFamily: root.fontFamily
          }
          PanelActionButton {
            focusable: true
            iconText: "⚙"
            tooltipText: "Settings"
            bordered: true
            foreground: root.foreground
            hoverColor: root.accent
            fontFamily: root.fontFamily
            fontSize: root.fontSize(Style.font.icon)
            onClicked: root.openSettings()
          }
        }

        PanelSeparator { Layout.fillWidth: true; foreground: root.foreground }

        // ---------------------------------------------------- scroll body
        Flickable {
          id: threadFlick
          Layout.fillWidth: true
          Layout.fillHeight: true
          contentWidth: width
          contentHeight: listContent.implicitHeight
          clip: true
          boundsBehavior: Flickable.StopAtBounds
          interactive: contentHeight > height
          ScrollBar.vertical: ScrollBar { policy: ScrollBar.AsNeeded }

          // Wheel scrolling is DIRECT, 1:1 — no animation. Two animated
          // schemes (restarted easing, SmoothedAnimation chase) both fought
          // the MX Master's hi-res event flood and felt broken; hi-res
          // wheels are smooth by HARDWARE, so applying each delta
          // immediately is what a browser does and what reads as smooth.
          // The handler owns the event outright so the Flickable's own
          // wheel path can never double-apply it.
          // MouseArea.onWheel, NOT WheelHandler: instrumentation proved the
          // WheelHandler never received a single event on this stack — every
          // "stride" tweak was a placebo and the Flickable's native kinetic
          // path (the decaying one) was doing the scrolling. Omarchy's own
          // panels use MouseArea.onWheel; it works. NoButton + z:-1 so it
          // never steals clicks/hover from the rows above it.
          MouseArea {
            anchors.fill: parent
            z: -1
            acceptedButtons: Qt.NoButton
            onWheel: function(wheel) {
              var d = wheel.pixelDelta.y !== 0 ? wheel.pixelDelta.y * 3.0 : wheel.angleDelta.y * 4.5
              var max = Math.max(0, threadFlick.contentHeight - threadFlick.height)
              threadFlick.contentY = Math.max(0, Math.min(max, threadFlick.contentY - d))
              wheel.accepted = true
            }
          }

          ColumnLayout {
            id: listContent
            width: parent.width
            spacing: root.inThread ? root.space(2) : root.space(root.splitView ? 10 : 6)

            // ------------------------------------------------- OFFLINE
            Text {
              Layout.fillWidth: true
              visible: !root.online
              text: "The Mac is not reachable, so there is no iMessage bridge right now. "
                  + "chat.db and the AppleScript send path both live on the Mac — this machine is only a client. "
                  + "Wake the Mac (or check the network) and Blip reconnects on its own."
              textFormat: Text.PlainText
              color: root.dim
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.bodySmall)
              wrapMode: Text.WordWrap
            }
            // Reachable but broken — say exactly what to fix (Full Disk Access,
            // Automation, blip-setup). collector.explainBridgeError writes it.
            Text {
              Layout.fillWidth: true
              visible: root.online && root.hostWidget && !root.hostWidget.healthy
                       && String(root.hostWidget.lastError || "") !== ""
              text: "⚠ " + String(root.hostWidget ? root.hostWidget.lastError : "")
              textFormat: Text.PlainText
              color: root.urgent
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.bodySmall)
              wrapMode: Text.WordWrap
            }

            // ---------------------------------------------- LIST VIEW
            RowLayout {
              Layout.fillWidth: true
              visible: root.online && root.listShowing
                       && (root.newMode || root.searchShowing || !root.splitView || root.unread > 0)
              PanelSectionHeader {
                Layout.fillWidth: true
                visible: root.newMode || root.searchShowing
                text: root.newMode ? "NEW MESSAGE" : "SEARCH"
                foreground: root.foreground
                fontFamily: root.fontFamily
                fontSize: root.fontSize(Style.font.caption)
              }
              // A real button (PanelActionButton = the stock panels' control).
              // The hand-rolled Text+MouseArea version lost its clicks to the
              // panel's dismiss layer — clicking it CLOSED the panel.
              PanelActionButton {
                visible: !root.newMode && !root.searchShowing && !root.splitView
                iconText: "＋"
                tooltipText: "New message (n)"
                bordered: true
                foreground: root.foreground
                hoverColor: root.accent
                fontFamily: root.fontFamily
                fontSize: root.fontSize(Style.font.icon)
                onClicked: root.startNew()
              }
              // Local only: moves readMark/readMarks in state.json so the
              // badge and dots clear. Nothing is written back to the Mac —
              // AppleScript cannot flip is_read (see "not possible" in CLAUDE.md).
              // TapHandler, not MouseArea: the thread rows' proven pattern —
              // the MouseArea version could lose clicks to the dismiss layer.
              Text {
                id: markAllBtn
                visible: root.unread > 0 && !root.searchShowing && !root.newMode
                text: "mark all read"
                textFormat: Text.PlainText
                color: markAllHover.hovered ? root.mineFill : root.cyan
                font.family: root.fontFamily
                font.pixelSize: root.fontSize(Style.font.caption)
                font.underline: markAllHover.hovered
                HoverHandler { id: markAllHover; cursorShape: Qt.PointingHandCursor }
                TapHandler { onTapped: root.markAllRead() }
              }
            }

            // ------------------------------------------------ SEARCH
            // The box is ALWAYS visible in list view — a hidden search
            // behind a key nobody presses is a search that does not exist
            // (Fred: "I don't see search"). Click it or press `/`.
            // ---------------------------------------- NEW CONVERSATION
            TextField {
              id: newField
              Layout.fillWidth: true
              visible: root.online && root.listShowing && root.newMode
              placeholderText: "name, number, or email — Enter searches contacts"
              foreground: root.foreground
              accent: root.accent
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.bodySmall)
              onAccepted: root.runContactSearch()
              Keys.onEscapePressed: root.exitNew()
            }

            Text {
              Layout.fillWidth: true
              visible: root.newMode && root.newNote !== ""
              text: root.newNote
              textFormat: Text.PlainText
              color: root.dim
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.caption)
            }

            Repeater {
              model: root.online && root.listShowing && root.newMode ? root.newResults : []
              delegate: Rectangle {
                required property var modelData
                Layout.fillWidth: true
                implicitHeight: contactRow.implicitHeight + root.space(12)
                radius: root.corner(Style.cornerRadius)
                color: contactHover.hovered
                  ? Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.08)
                  : "transparent"
                HoverHandler { id: contactHover }
                TapHandler { onTapped: root.openContact(modelData) }
                RowLayout {
                  id: contactRow
                  anchors.left: parent.left
                  anchors.right: parent.right
                  anchors.verticalCenter: parent.verticalCenter
                  anchors.leftMargin: root.space(8)
                  anchors.rightMargin: root.space(8)
                  spacing: root.space(8)
                  Text {
                    text: String(modelData.name || "")
                    textFormat: Text.PlainText
                    elide: Text.ElideRight
                    color: root.foreground
                    font.family: root.fontFamily
                    font.pixelSize: root.fontSize(Style.font.bodySmall)
                    font.bold: true
                  }
                  Text {
                    Layout.fillWidth: true
                    text: String(modelData.handle || "") + "  ·  " + String(modelData.kind || "")
                    textFormat: Text.PlainText
                    elide: Text.ElideRight
                    color: root.dim
                    font.family: root.fontFamily
                    font.pixelSize: root.fontSize(Style.font.caption)
                  }
                }
              }
            }

            TextField {
              id: searchField
              Layout.fillWidth: true
              visible: root.online && root.listShowing && !root.newMode
              placeholderText: "🔍 search all messages"
              foreground: root.foreground
              accent: root.accent
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.bodySmall)
              onAccepted: root.runSearch()
              onActiveFocusChanged: if (activeFocus && !root.searching) root.searching = true
              Keys.onEscapePressed: root.exitSearch()
            }

            Text {
              Layout.fillWidth: true
              visible: root.searching && root.searchNote !== ""
              text: root.searchNote
              textFormat: Text.PlainText
              color: root.dim
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.caption)
            }

            Repeater {
              model: root.online && root.listShowing && root.searchShowing ? root.searchResults : []
              delegate: Rectangle {
                required property var modelData
                Layout.fillWidth: true
                implicitHeight: hitCol.implicitHeight + root.space(12)
                radius: root.corner(Style.cornerRadius)
                color: hitHover.hovered
                  ? Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.08)
                  : "transparent"
                HoverHandler { id: hitHover }
                TapHandler { onTapped: root.openSearchHit(modelData) }

                ColumnLayout {
                  id: hitCol
                  anchors.left: parent.left
                  anchors.right: parent.right
                  anchors.verticalCenter: parent.verticalCenter
                  anchors.leftMargin: root.space(8)
                  anchors.rightMargin: root.space(8)
                  spacing: root.space(2)
                  RowLayout {
                    Layout.fillWidth: true
                    Text {
                      Layout.fillWidth: true
                      text: String(modelData.name || modelData.chat)
                            + (modelData.group ? "  ·  group" : "")
                      textFormat: Text.PlainText
                      elide: Text.ElideRight
                      color: root.foreground
                      font.family: root.fontFamily
                      font.pixelSize: root.fontSize(Style.font.bodySmall)
                      font.bold: true
                    }
                    Text {
                      text: String(modelData.ts || "").slice(0, 16)
                      textFormat: Text.PlainText
                      color: root.dim
                      font.family: root.fontFamily
                      font.pixelSize: root.fontSize(Style.font.caption)
                    }
                  }
                  Text {
                    Layout.fillWidth: true
                    text: (modelData.from_me ? "you: " : "") + String(modelData.text || "")
                    textFormat: Text.PlainText
                    elide: Text.ElideRight
                    maximumLineCount: 2
                    wrapMode: Text.WordWrap
                    color: root.dim
                    font.family: root.fontFamily
                    font.pixelSize: root.fontSize(Style.font.caption)
                  }
                }
              }
            }

            // Messages.app's pinning plist supplies both membership and order.
            // Keep the same visual grammar: large circular contacts in a grid,
            // then the remaining conversations in chronological rows.
            ColumnLayout {
              Layout.fillWidth: true
              visible: root.online && root.listShowing && !root.searchShowing
                       && !root.newMode && root.pinnedThreads.length > 0
              // Match the header-to-search breathing room: listContent's
              // spacing supplies most of this gap and this margin supplies
              // the difference from the outer divider spacing.
              Layout.topMargin: root.space(root.splitView ? 4 : 2)
              spacing: root.space(7)

              GridLayout {
                id: pinnedGrid
                Layout.fillWidth: true
                columns: root.splitView && root.sidebarWidth < 400 ? 3 : 4
                columnSpacing: root.space(6)
                rowSpacing: root.space(8)

                Repeater {
                  model: root.pinnedThreads
                  delegate: PinnedConversation {
                    required property var modelData
                    required property int index
                    Layout.fillWidth: true
                    Layout.preferredHeight: pinAvatarSize + root.space(38)
                    thread: modelData
                    selected: root.cursor === index
                  }
                }
              }
            }

            Text {
              Layout.fillWidth: true
              visible: root.online && root.listShowing && !root.searchShowing && !root.newMode && root.threads.length === 0
              text: "No threads in the current window yet — press r to refresh."
              textFormat: Text.PlainText
              color: root.dim
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.bodySmall)
              wrapMode: Text.WordWrap
            }

            Repeater {
              model: root.online && root.listShowing && !root.searchShowing && !root.newMode ? root.regularThreads : []
              delegate: Rectangle {
                required property var modelData
                required property int index

                Layout.fillWidth: true
                implicitHeight: rowRow.implicitHeight + root.space(root.splitView ? 20 : 12)
                radius: root.corner(Style.cornerRadius)
                color: rowHover.hovered || root.cursor === root.pinnedThreads.length + index
                  ? Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.08)
                  : "transparent"

                HoverHandler { id: rowHover }
                TapHandler { onTapped: root.openThread(modelData) }
                TapHandler {
                  acceptedButtons: Qt.RightButton
                  onTapped: root.openContactContext(modelData, "")
                }

                RowLayout {
                  id: rowRow
                  anchors.fill: parent
                  anchors.margins: root.space(6)
                  spacing: root.space(8)

                  // the iMessage blue dot — present only while the thread has
                  // unread inbound; the slot stays so names line up.
                  Rectangle {
                    width: root.space(9); height: width; radius: width / 2
                    color: root.mineFill
                    opacity: modelData.unread > 0 ? 1 : 0
                  }

                  // avatar circle — the contact's photo when Contacts has one,
                  // initials otherwise (the iMessage sidebar look)
                  Rectangle {
                    id: avatarCircle
                    width: Math.max(1, Math.round(Style.spaceReal(root.avatarSize))); height: width; radius: width / 2
                    color: Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.18)
                    readonly property string avatarHandle: String(modelData.handle || modelData.chat || "")
                    Component.onCompleted: if (!root.isGroupId(String(modelData.chat || ""))) root.requestAvatar(avatarHandle)
                    Image {
                      id: avatarImg
                      anchors.fill: parent
                      visible: false
                      source: root.avatarFiles[avatarCircle.avatarHandle] || ""
                      asynchronous: true
                      fillMode: Image.PreserveAspectCrop
                      sourceSize.width: 96
                      sourceSize.height: 96
                      // a stale/corrupt cache file → initials, and no retry this session
                      onStatusChanged: if (status === Image.Error && avatarCircle.avatarHandle !== "") {
                        var m = Object.assign({}, root.avatarFiles); m[avatarCircle.avatarHandle] = ""; root.avatarFiles = m
                      }
                    }
                    Item {
                      id: avatarMask
                      anchors.fill: parent
                      visible: false
                      layer.enabled: true
                      Rectangle { anchors.fill: parent; radius: width / 2 }
                    }
                    MultiEffect {
                      anchors.fill: parent
                      source: avatarImg
                      visible: avatarImg.status === Image.Ready
                      maskEnabled: true
                      maskSource: avatarMask
                    }
                    Text {
                      anchors.centerIn: parent
                      visible: avatarImg.status !== Image.Ready
                      text: {
                        var n = String(modelData.name || "")
                        if (/^[+0-9]/.test(n) || n === "") return "#"
                        var parts = n.trim().split(/\s+/)
                        return (parts[0][0] + (parts.length > 1 ? parts[parts.length - 1][0] : "")).toUpperCase()
                      }
                      color: root.foreground
                      font.family: root.fontFamily
                      font.pixelSize: root.fontSize(Style.font.caption)
                      font.bold: true
                    }
                  }

                  ColumnLayout {
                    Layout.fillWidth: true
                    spacing: root.space(1)
                    RowLayout {
                      Layout.fillWidth: true
                      spacing: root.space(6)
                      Text {
                        Layout.fillWidth: true
                        text: String(modelData.name || modelData.chat)
                        textFormat: Text.PlainText
                        elide: Text.ElideRight
                        color: root.foreground
                        font.family: root.fontFamily
                        font.pixelSize: root.fontSize(Style.font.bodySmall)
                        font.bold: modelData.unread > 0
                      }
                      Text {
                        text: root.fmtTime(modelData.last_ts)
                        textFormat: Text.PlainText
                        color: modelData.unread > 0 ? root.mineFill : root.dim
                        font.family: root.fontFamily
                        font.pixelSize: root.fontSize(Style.font.caption)
                      }
                    }
                    Text {
                      Layout.fillWidth: true
                      text: (modelData.last_from_me ? "You: " : "") + String(modelData.last_text || "")
                      textFormat: Text.PlainText
                      elide: Text.ElideRight
                      maximumLineCount: 1
                      color: root.dim
                      font.family: root.fontFamily
                      font.pixelSize: root.fontSize(Style.font.caption)
                    }
                  }

                }

                // Messages separates chronological conversations with a
                // hairline that begins after the avatar rather than cutting
                // through the unread-dot/avatar gutter.
                Rectangle {
                  anchors.left: parent.left
                  anchors.leftMargin: root.space(6 + 9 + 8) + avatarCircle.width + root.space(8)
                  anchors.right: parent.right
                  anchors.rightMargin: root.space(6)
                  anchors.bottom: parent.bottom
                  height: Math.max(1, Math.round(Style.spaceReal(1)))
                  visible: index < root.regularThreads.length - 1
                  color: Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.10)
                }
              }
            }

          }
        }
      }
    }

    Rectangle {
      visible: root.splitView
      Layout.preferredWidth: 1
      Layout.fillHeight: true
      color: Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.12)
    }

    // ------------------------------------------------- conversation pane
    Item {
      id: conversationPane
      visible: root.splitView || root.inThread
      Layout.fillHeight: true
      Layout.fillWidth: true
      ColumnLayout {
        anchors.fill: parent
        // Gutters for the app: the popout's card supplies its own padding,
        // the window's panes had text flush against the borders (Fred).
        anchors.leftMargin: root.splitView ? root.space(18) : 0
        anchors.rightMargin: root.splitView ? root.space(18) : 0
        anchors.topMargin: root.splitView ? root.space(10) : 0
        anchors.bottomMargin: root.splitView ? root.space(10) : 0
        spacing: root.space(8)
        RowLayout {
          Layout.fillWidth: true
          spacing: root.space(8)
          PanelHero {
            Layout.fillWidth: true
            // Pinned group names are Messages' user-facing title. `name` can
            // still be the opaque chatNNN id carried by message rows.
            title: root.inThread
              ? String(root.active.pin_name || root.active.name || root.active.chat)
              : "Select a conversation"
            meta: root.inThread
              ? (root.activeIsGroup
                  ? (root.isSendable(root.active) ? "group" : "group · read-only (id unknown)")
                  : String(root.active.handle))
              : ""
            detail: root.inThread
              ? (root.loading ? "loading…" : (root.splitView ? "" : "Esc = back"))
              : ""
            foreground: root.foreground
            fontFamily: root.fontFamily
          }
          // The app's NEW button lives up here (where "Esc = back" used to be).
          PanelActionButton {
            visible: root.splitView && !root.newMode
            Layout.alignment: Qt.AlignTop
            Layout.topMargin: root.space(6)
            iconText: "＋"
            tooltipText: "New message (n)"
            bordered: true
            foreground: root.foreground
            hoverColor: root.accent
            fontFamily: root.fontFamily
            fontSize: root.fontSize(Style.font.icon)
            onClicked: root.startNew()
          }
        }

        PanelSeparator { Layout.fillWidth: true; foreground: root.foreground }

        // ---------------------------------------------------- scroll body
        Flickable {
          id: flick
          Layout.fillWidth: true
          Layout.fillHeight: true
          contentWidth: width
          contentHeight: content.implicitHeight
          clip: true
          boundsBehavior: Flickable.StopAtBounds
          interactive: contentHeight > height
          ScrollBar.vertical: ScrollBar { policy: ScrollBar.AsNeeded }
          // stick-to-bottom (BlueFerry's pattern): once pinned, KEEP the
          // view at the bottom through late content growth — async images
          // finishing their decode were pushing the newest bubble below
          // the fold. Scrolling up releases the stick; scrolling back to
          // the end re-arms it.
          property bool stick: false
          onContentHeightChanged: {
            if (!root.inThread) return
            if (root.pinToBottom) {
              stick = true
              contentY = Math.max(0, contentHeight - height)
              if (!root.loading) root.pinToBottom = false
            } else if (stick) {
              contentY = Math.max(0, contentHeight - height)
            }
          }
          onMovementStarted: stick = false
          onMovementEnded: stick = atYEnd
          // A reload deferred while the user read history fires the moment
          // they return to the bottom.
          onStickChanged: if (stick && root.pushPending) {
            root.pushPending = false
            Qt.callLater(root.pushReload)
          }

          // Wheel scrolling is DIRECT, 1:1 — no animation. Two animated
          // schemes (restarted easing, SmoothedAnimation chase) both fought
          // the MX Master's hi-res event flood and felt broken; hi-res
          // wheels are smooth by HARDWARE, so applying each delta
          // immediately is what a browser does and what reads as smooth.
          // The handler owns the event outright so the Flickable's own
          // wheel path can never double-apply it.
          // MouseArea.onWheel, NOT WheelHandler: instrumentation proved the
          // WheelHandler never received a single event on this stack — every
          // "stride" tweak was a placebo and the Flickable's native kinetic
          // path (the decaying one) was doing the scrolling. Omarchy's own
          // panels use MouseArea.onWheel; it works. NoButton + z:-1 so it
          // never steals clicks/hover from the rows above it.
          MouseArea {
            anchors.fill: parent
            z: -1
            acceptedButtons: Qt.NoButton
            onWheel: function(wheel) {
              var d = wheel.pixelDelta.y !== 0 ? wheel.pixelDelta.y * 3.0 : wheel.angleDelta.y * 4.5
              var max = Math.max(0, flick.contentHeight - flick.height)
              flick.contentY = Math.max(0, Math.min(max, flick.contentY - d))
              // the wheel bypasses Flickable movement signals — maintain the
              // bottom-stick here too
              flick.stick = flick.contentY >= max - 4
              wheel.accepted = true
            }
          }

          ColumnLayout {
            id: content
            width: parent.width
            spacing: root.inThread ? root.space(2) : root.space(6)

            // ------------------------------------------- CONVERSATION
            Text {
              Layout.fillWidth: true
              visible: root.inThread && root.loading
              text: "loading…"
              horizontalAlignment: Text.AlignHCenter
              color: root.dim
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.caption)
            }

            Repeater {
              model: root.inThread ? root.bubbles : []
              delegate: ColumnLayout {
                id: bubbleRow
                required property var modelData
                readonly property bool mine: modelData.from_me === true
                readonly property bool incomingGroup: root.activeIsGroup && !mine
                readonly property bool runEndsHere: incomingGroup && modelData.groupEnd === true
                readonly property bool hasTextBubble:
                  !modelData.retracted &&
                  (String(modelData.text || "") !== "" || (modelData.attachments || []).length === 0) &&
                  modelData.linkOnly !== true
                readonly property bool hasLinkCard: !modelData.retracted && !!modelData.link
                readonly property int attachmentCount:
                  modelData.retracted ? 0 : (modelData.attachments || []).length

                Layout.fillWidth: true
                spacing: root.space(2)

                TapHandler {
                  acceptedButtons: Qt.RightButton
                  onTapped: root.openContactContext(root.active, String(modelData.text || ""))
                }

                // day divider — "Today", "Yesterday", "Aug 28"
                Text {
                  Layout.fillWidth: true
                  visible: String(modelData.day || "") !== ""
                  text: String(modelData.day || "")
                  horizontalAlignment: Text.AlignHCenter
                  color: root.dim
                  font.family: root.fontFamily
                  font.pixelSize: root.fontSize(Style.font.caption)
                  font.bold: true
                  topPadding: root.space(10)
                  bottomPadding: root.space(4)
                }

                // in a group, iMessage names the sender above each run of theirs
                Text {
                  Layout.alignment: Qt.AlignLeft
                  Layout.leftMargin: root.space(10)
                    + (bubbleRow.incomingGroup ? root.groupMessageAvatarSlot : 0)
                  Layout.topMargin: root.space(6)
                  visible: root.activeIsGroup && !bubbleRow.mine && modelData.groupStart === true
                  text: String(modelData.name || "")
                  textFormat: Text.PlainText
                  color: root.dim
                  font.family: root.fontFamily
                  font.pixelSize: root.fontSize(Style.font.caption)
                }

                // "You unsent a message" tombstone replaces a retracted bubble
                RowLayout {
                  Layout.fillWidth: true
                  visible: modelData.retracted === true
                  spacing: 0
                  Item { Layout.fillWidth: true; visible: bubbleRow.mine }
                  GroupMessageAvatarSlot {
                    reserve: bubbleRow.incomingGroup
                    handle: String(modelData.handle || "")
                    name: String(modelData.name || "")
                    showAvatar: bubbleRow.runEndsHere
                  }
                  Text {
                    text: (bubbleRow.mine ? "You" : String(modelData.name || "They")) + " unsent a message"
                    textFormat: Text.PlainText
                    color: root.dim
                    font.family: root.fontFamily
                    font.pixelSize: root.fontSize(Style.font.caption)
                    font.italic: true
                    padding: root.space(4)
                  }
                  Item { Layout.fillWidth: true; visible: !bubbleRow.mine }
                }

                // inline-reply context: the quoted snippet, dimmed, above the bubble
                RowLayout {
                  Layout.fillWidth: true
                  visible: !modelData.retracted && String(modelData.replyText || "") !== ""
                  Layout.topMargin: modelData.groupStart ? root.space(6) : 0
                  spacing: 0
                  Item { Layout.fillWidth: true; visible: bubbleRow.mine }
                  GroupMessageAvatarSlot {
                    reserve: bubbleRow.incomingGroup
                    handle: String(modelData.handle || "")
                    name: String(modelData.name || "")
                    showAvatar: false
                  }
                  Rectangle {
                    Layout.preferredWidth: Math.min(Math.ceil(replySnippet.implicitWidth) + root.space(18), Math.round(content.width * 0.7))
                    Layout.preferredHeight: Math.ceil(replySnippet.implicitHeight) + root.space(10)
                    radius: root.corner(root.space(12))
                    color: "transparent"
                    border.color: root.dim
                    border.width: 1
                    opacity: 0.75
                    Text {
                      id: replySnippet
                      x: root.space(9); y: root.space(5)
                      width: Math.round(content.width * 0.7) - root.space(18)
                      text: "↩ " + (modelData.replyMine ? "You: " : "") + String(modelData.replyText || "")
                      textFormat: Text.PlainText
                      elide: Text.ElideRight
                      maximumLineCount: 1
                      color: root.dim
                      font.family: root.fontFamily
                      font.pixelSize: root.fontSize(Style.font.caption)
                    }
                  }
                  Item { Layout.fillWidth: true; visible: !bubbleRow.mine }
                }

                // attachment chips — metadata only, the file itself is on the
                // Mac. One chip per ROW: a single horizontal run of chips once
                // summed its implicit widths into the delegate and stretched
                // the whole column past the panel edge (a URL message can
                // carry several attachments).
                Repeater {
                  model: modelData.retracted ? [] : (modelData.attachments || [])
                  delegate: RowLayout {
                    id: chipRow
                    required property var modelData
                    required property int index
                    // THE scroll killer (Fred: "each scroll gets smaller and
                    // smaller until it stops"): while reading history, image
                    // fetches complete and each chip ABOVE the viewport grows
                    // ~10× — content expansion visually cancels every wheel
                    // motion. Compensate: when this row grows above the
                    // viewport top, shift contentY by the same delta so the
                    // reader's position is anchored.
                    property real prevH: -1
                    onHeightChanged: {
                      if (prevH < 0) { prevH = height; return }
                      var d = height - prevH
                      prevH = height
                      if (d === 0 || flick.stick || root.pinToBottom) return
                      var yc = chipRow.mapToItem(content, 0, 0).y
                      if (yc < flick.contentY)
                        flick.contentY = Math.max(0, flick.contentY + d)
                    }
                    readonly property string attId: String(modelData.id || "")
                    // undefined = not fetched, "" = failed, else file:// url
                    readonly property var fileUrl: root.attFiles[chipRow.attId]
                    readonly property var imageMetrics: root.attMetrics[chipRow.attId] || ({})
                    readonly property bool failed: chipRow.fileUrl === ""
                    readonly property bool showImage:
                      root.isImageMime(chipRow.modelData.mime) &&
                      chipRow.fileUrl !== undefined && chipRow.fileUrl !== ""
                    Layout.fillWidth: true
                    Layout.topMargin: index === 0 && bubbleRow.modelData.groupStart ? root.space(6) : 0
                    spacing: 0
                    Item { Layout.fillWidth: true; visible: bubbleRow.mine }
                    GroupMessageAvatarSlot {
                      reserve: bubbleRow.incomingGroup
                      handle: String(bubbleRow.modelData.handle || "")
                      name: String(bubbleRow.modelData.name || "")
                      showAvatar: bubbleRow.runEndsHere &&
                        !bubbleRow.hasTextBubble && !bubbleRow.hasLinkCard &&
                        index === bubbleRow.attachmentCount - 1
                    }

                    // fetched image renders inline, like Messages; click = full view
                    Image {
                      id: attImage
                      visible: chipRow.showImage
                      // A phone screenshot can be thousands of source pixels
                      // wide. Size media in logical UI units so a wide desktop
                      // window (especially on a 2× display) does not turn it
                      // into a nearly full-width wall.
                      readonly property real maxW: Math.min(
                        Math.round(content.width * 0.6), root.space(380)
                      )
                      readonly property real pixelRatio:
                        Number(chipRow.imageMetrics.pixelRatio || 1)
                      readonly property real naturalWidth:
                        Number(chipRow.imageMetrics.pixelWidth || 0) > 0
                          ? Number(chipRow.imageMetrics.pixelWidth) / pixelRatio
                          : implicitWidth
                      readonly property real naturalHeight:
                        Number(chipRow.imageMetrics.pixelHeight || 0) > 0
                          ? Number(chipRow.imageMetrics.pixelHeight) / pixelRatio
                          : implicitHeight
                      source: chipRow.showImage ? chipRow.fileUrl : ""
                      asynchronous: true
                      fillMode: Image.PreserveAspectFit
                      // bound the DECODE in BOTH axes, not just the paint — a
                      // 12MP photo (or a 100×100000 sliver) must not cost
                      // 50 MB of texture (Codex review points 19 and #3)
                      sourceSize.width: 800
                      sourceSize.height: 800
                      // LRU eviction or a corrupt file: fall back to the chip
                      // (⚠ marker); a click re-fetches through fetch.ts.
                      onStatusChanged: if (status === Image.Error) {
                        var m = Object.assign({}, root.attFiles)
                        m[chipRow.attId] = ""
                        root.attFiles = m
                      }
                      Layout.preferredWidth: status === Image.Ready ? Math.min(maxW, naturalWidth) : maxW
                      Layout.preferredHeight: status === Image.Ready && naturalWidth > 0
                        ? Layout.preferredWidth * naturalHeight / naturalWidth
                        : root.space(120)
                      HoverHandler { cursorShape: Qt.PointingHandCursor }
                      TapHandler { onTapped: root.openAttachment(chipRow.modelData) }
                    }

                    Rectangle {
                      visible: !chipRow.showImage
                      Layout.preferredWidth: Math.ceil(chipText.implicitWidth) + root.space(18)
                      Layout.preferredHeight: Math.ceil(chipText.implicitHeight) + root.space(12)
                      radius: root.corner(root.space(14))
                      color: bubbleRow.mine ? root.mineFill : root.theirsFill
                      opacity: 0.85
                      Text {
                        id: chipText
                        anchors.centerIn: parent
                        text: (chipRow.failed ? "⚠ " : "") +
                              (root.fetchingId === chipRow.attId ? "⏳ " : "") +
                              root.attachmentIcon(chipRow.modelData.mime) + " " +
                              (String(chipRow.modelData.name || "").length > 32
                                ? String(chipRow.modelData.name).slice(0, 29) + "…"
                                : String(chipRow.modelData.name || "file"))
                        textFormat: Text.PlainText
                        color: bubbleRow.mine ? root.mineText : root.theirsText
                        font.family: root.fontFamily
                        font.pixelSize: root.fontSize(Style.font.caption)
                      }
                      HoverHandler { cursorShape: Qt.PointingHandCursor }
                      TapHandler { onTapped: root.openAttachment(chipRow.modelData) }
                    }
                    Item { Layout.fillWidth: true; visible: !bubbleRow.mine }
                  }
                }

                // rich-link card (URL balloons): preview image + title + host;
                // click opens the link. A message that is ONLY the URL shows
                // just the card, like Messages.
                RowLayout {
                  id: linkRow
                  visible: !modelData.retracted && !!modelData.link
                  Layout.fillWidth: true
                  Layout.topMargin: (modelData.groupStart ? root.space(6) : 0)
                    + ((modelData.tapbacks || []).length > 0
                      ? root.space(23) : 0)
                  spacing: 0
                  // same anchoring as the chips: a card image completing ABOVE
                  // the viewport must not shove the reader's position.
                  property real prevH: -1
                  onHeightChanged: {
                    if (prevH < 0) { prevH = height; return }
                    var d = height - prevH
                    prevH = height
                    if (d === 0 || flick.stick || root.pinToBottom) return
                    var yc = linkRow.mapToItem(content, 0, 0).y
                    if (yc < flick.contentY)
                      flick.contentY = Math.max(0, flick.contentY + d)
                  }
                  Item { Layout.fillWidth: true; visible: bubbleRow.mine }
                  GroupMessageAvatarSlot {
                    reserve: bubbleRow.incomingGroup
                    handle: String(modelData.handle || "")
                    name: String(modelData.name || "")
                    showAvatar: bubbleRow.runEndsHere && !bubbleRow.hasTextBubble
                  }
                  Item {
                    id: linkCardWrap
                    Layout.preferredWidth: Math.min(Math.round(content.width * 0.62), root.space(380))
                    Layout.preferredHeight: linkCard.height

                    Rectangle {
                      id: linkCard
                      width: parent.width
                      height: linkCol.implicitHeight
                      readonly property var link: modelData.link || ({})
                      readonly property string imgUrl: link.image_id ? String(root.attFiles[String(link.image_id)] || "") : ""
                      radius: root.corner(root.space(14))
                      clip: true
                      color: bubbleRow.mine ? root.mineFill : root.theirsFill
                      ColumnLayout {
                        id: linkCol
                        width: parent.width
                        spacing: 0
                        Image {
                          id: linkImage
                          visible: linkCard.imgUrl !== "" && status === Image.Ready
                          Layout.fillWidth: true
                          // Link artwork is often portrait or square. Preserve
                          // its natural aspect ratio instead of clipping every
                          // preview to the old shallow 220-unit banner.
                          Layout.preferredHeight: visible && implicitWidth > 0
                            ? Math.min(root.space(480),
                                Math.round(linkCard.width * implicitHeight / implicitWidth))
                            : 0
                          source: linkCard.imgUrl
                          asynchronous: true
                          fillMode: Image.PreserveAspectFit
                          sourceSize.width: 960
                          sourceSize.height: 960
                        }
                        ColumnLayout {
                          Layout.fillWidth: true
                          Layout.margins: root.space(10)
                          spacing: root.space(2)
                          Text {
                            Layout.fillWidth: true
                            visible: text !== ""
                            text: String(linkCard.link.title || "")
                            textFormat: Text.PlainText
                            wrapMode: Text.Wrap
                            maximumLineCount: 2
                            elide: Text.ElideRight
                            color: bubbleRow.mine ? root.mineText : root.theirsText
                            font.family: root.fontFamily
                            font.pixelSize: root.fontSize(Style.font.bodySmall)
                            font.bold: true
                          }
                          Text {
                            Layout.fillWidth: true
                            visible: text !== ""
                            text: String(linkCard.link.summary || "")
                            textFormat: Text.PlainText
                            wrapMode: Text.Wrap
                            maximumLineCount: 2
                            elide: Text.ElideRight
                            color: bubbleRow.mine ? root.mineText : root.theirsText
                            opacity: 0.85
                            font.family: root.fontFamily
                            font.pixelSize: root.fontSize(Style.font.caption)
                          }
                          Text {
                            Layout.fillWidth: true
                            text: root.linkHost(String(linkCard.link.url || ""))
                            textFormat: Text.PlainText
                            elide: Text.ElideRight
                            color: bubbleRow.mine ? root.mineText : root.theirsText
                            opacity: 0.6
                            font.family: root.fontFamily
                            font.pixelSize: root.fontSize(Style.font.caption)
                          }
                        }
                      }
                      HoverHandler { cursorShape: Qt.PointingHandCursor }
                      TapHandler { onTapped: root.openLink(String(linkCard.link.url || "")) }
                    }

                    TapbackReaction {
                      mine: bubbleRow.mine
                      // Messages anchors the reaction to the unfurled card,
                      // including when the shared URL also has a caption.
                      reactions: modelData.tapbacks || []
                    }
                  }
                  Item { Layout.fillWidth: true; visible: !bubbleRow.mine }
                }

                // the bubble in an explicit spacer row: a stretchy Item on
                // the sender's far side guarantees right/left placement even
                // when the delegate's own width collapses to its content.
                RowLayout {
                  Layout.fillWidth: true
                  // A Messages-sized tapback pill overlaps the top edge —
                  // reserve its raised portion so adjacent runs stay clear.
                  Layout.topMargin: (modelData.groupStart ? root.space(6) : 0)
                                    + ((modelData.tapbacks || []).length > 0 && !modelData.link
                                      ? root.space(23) : 0)
                  visible: !modelData.retracted &&
                           (String(modelData.text || "") !== "" || (modelData.attachments || []).length === 0) &&
                           modelData.linkOnly !== true
                  spacing: 0

                  Item { Layout.fillWidth: true; visible: bubbleRow.mine }
                  GroupMessageAvatarSlot {
                    reserve: bubbleRow.incomingGroup
                    handle: String(modelData.handle || "")
                    name: String(modelData.name || "")
                    showAvatar: bubbleRow.runEndsHere
                  }

                  Item {
                    id: bubble
                    readonly property bool expressiveEmoji: modelData.emojiOnly === true
                    readonly property real maxInner: Math.round(content.width * 0.78) - root.space(22)
                    readonly property real bubbleRadius: root.corner(root.space(16))
                    readonly property color color: bubbleRow.mine ? root.mineFill : root.theirsFill
                    Layout.preferredWidth: Math.ceil(bubbleText.contentWidth)
                      + (expressiveEmoji ? 0 : root.space(22))
                    Layout.preferredHeight: Math.ceil(bubbleText.contentHeight)
                      + (expressiveEmoji ? 0 : root.space(14))

                    // One path draws the rounded bubble and its squared sender
                    // corner. The old translucent Rectangle + overlapping
                    // corner Rectangle composited twice and made that corner
                    // visibly darker on incoming bubbles.
                    Shape {
                      visible: !bubble.expressiveEmoji
                      anchors.fill: parent
                      antialiasing: true
                      ShapePath {
                        id: bubblePath
                        readonly property real r: Math.min(
                          bubble.bubbleRadius, bubble.width / 2, bubble.height / 2
                        )
                        readonly property bool squareRight:
                          modelData.groupEnd === true && bubbleRow.mine
                        readonly property bool squareLeft:
                          modelData.groupEnd === true && !bubbleRow.mine
                        strokeColor: "transparent"
                        fillColor: bubble.color
                        startX: r
                        startY: 0
                        PathLine { x: bubble.width - bubblePath.r; y: 0 }
                        PathQuad {
                          x: bubble.width; y: bubblePath.r
                          controlX: bubble.width; controlY: 0
                        }
                        PathLine {
                          x: bubble.width
                          y: bubblePath.squareRight ? bubble.height : bubble.height - bubblePath.r
                        }
                        PathQuad {
                          x: bubblePath.squareRight ? bubble.width : bubble.width - bubblePath.r
                          y: bubble.height
                          controlX: bubble.width; controlY: bubble.height
                        }
                        PathLine {
                          x: bubblePath.squareLeft ? 0 : bubblePath.r
                          y: bubble.height
                        }
                        PathQuad {
                          x: 0
                          y: bubblePath.squareLeft ? bubble.height : bubble.height - bubblePath.r
                          controlX: 0; controlY: bubble.height
                        }
                        PathLine { x: 0; y: bubblePath.r }
                        PathQuad {
                          x: bubblePath.r; y: 0
                          controlX: 0; controlY: 0
                        }
                      }
                    }

                    // TextEdit, not Text: read-only but selectable, so a message
                    // can be highlighted and Ctrl+C'd like any other text.
                    TextEdit {
                      id: bubbleText
                      x: bubble.expressiveEmoji ? 0 : root.space(11)
                      y: bubble.expressiveEmoji ? 0 : root.space(7)
                      width: bubble.maxInner
                      // html is pre-escaped + linkified in thread.ts (tested);
                      // plain messages keep the cheap PlainText path.
                      readonly property bool hasLink: String(modelData.html || "") !== ""
                      text: hasLink
                        ? root.richMessageHtml(
                            modelData.html,
                            bubbleRow.mine ? root.mineText : root.theirsText
                          )
                        : String(modelData.text || "")
                      textFormat: hasLink ? TextEdit.RichText : TextEdit.PlainText
                      onLinkActivated: function(link) {
                        openProc.command = ["xdg-open", link]
                        openProc.running = true
                      }
                      wrapMode: TextEdit.Wrap
                      readOnly: true
                      selectByMouse: true
                      persistentSelection: false
                      color: bubbleRow.mine ? root.mineText : root.theirsText
                      selectionColor: bubbleRow.mine ? "#ffffff" : root.mineFill
                      selectedTextColor: bubbleRow.mine ? root.mineFill : "#ffffff"
                      font.family: root.fontFamily
                      // macOS renders one-to-three emoji as expressive content:
                      // roughly four body-text heights and without a chat bubble.
                      font.pixelSize: bubble.expressiveEmoji
                        ? Math.round(root.fontSize(Style.font.bodySmall) * 4.1)
                        : root.fontSize(Style.font.bodySmall)
                      onActiveFocusChanged: root.bubbleFocused = activeFocus
                      Keys.onEscapePressed: { deselect(); composeField.forceActiveFocus() }
                    }

                    // tapback pill overlapping the corner opposite the tail
                    TapbackReaction {
                      mine: bubbleRow.mine
                      reactions: modelData.link ? [] : (modelData.tapbacks || [])
                    }
                  }

                  Item { Layout.fillWidth: true; visible: !bubbleRow.mine }
                }

                // timestamp under the last bubble of a run, same spacer trick;
                // carries the "Edited" and "sent with <effect>" tags too
                RowLayout {
                  Layout.fillWidth: true
                  visible: String(modelData.time || "") !== "" ||
                           modelData.edited === true || String(modelData.effect || "") !== "" ||
                           modelData.failed === true
                  spacing: 0
                  Item { Layout.fillWidth: true; visible: bubbleRow.mine }
                  GroupMessageAvatarSlot {
                    reserve: bubbleRow.incomingGroup
                    handle: String(modelData.handle || "")
                    name: String(modelData.name || "")
                    showAvatar: false
                  }
                  Text {
                    Layout.rightMargin: bubbleRow.mine ? root.space(6) : 0
                    Layout.leftMargin: bubbleRow.mine ? 0 : root.space(6)
                    text: [modelData.failed === true ? "⚠ Not Delivered" : "",
                           String(modelData.time || ""),
                           modelData.edited === true ? "Edited" : "",
                           String(modelData.effect || "") !== "" ? "sent with " + modelData.effect : ""]
                          .filter(function(s) { return s !== "" }).join(" · ")
                    textFormat: Text.PlainText
                    // a failed send is the one thing here that must not be dim
                    color: modelData.failed === true ? root.urgent : root.dim
                    font.bold: modelData.failed === true
                    font.family: root.fontFamily
                    font.pixelSize: root.fontSize(Style.font.caption)
                    bottomPadding: root.space(4)
                  }
                  Item { Layout.fillWidth: true; visible: !bubbleRow.mine }
                }

                // "Read 4:42 PM" — only ever under the newest read from-me bubble
                RowLayout {
                  Layout.fillWidth: true
                  visible: String(modelData.receipt || "") !== ""
                  spacing: 0
                  Item { Layout.fillWidth: true }
                  Text {
                    Layout.rightMargin: root.space(6)
                    text: String(modelData.receipt || "")
                    textFormat: Text.PlainText
                    color: root.dim
                    font.family: root.fontFamily
                    font.pixelSize: root.fontSize(Style.font.caption)
                    font.bold: true
                    bottomPadding: root.space(4)
                  }
                }

              }
            }

            Text {
              Layout.fillWidth: true
              visible: root.inThread && !root.loading && root.bubbles.length === 0
              text: "No messages loaded for this thread."
              horizontalAlignment: Text.AlignHCenter
              textFormat: Text.PlainText
              color: root.dim
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.bodySmall)
            }
          }
        }
        // ------------------------------------------------------ COMPOSE
        PanelSeparator {
          Layout.fillWidth: true
          visible: root.inThread
          foreground: root.foreground
        }

        // queued attachment — one per message; ✕ removes it
        RowLayout {
          Layout.fillWidth: true
          visible: root.inThread && root.draftPath !== ""
          spacing: 0
          Rectangle {
            Layout.preferredWidth: Math.ceil(draftText.implicitWidth) + root.space(18)
            Layout.preferredHeight: Math.ceil(draftText.implicitHeight) + root.space(12)
            radius: root.corner(root.space(14))
            color: root.mineFill
            opacity: 0.9
            Text {
              id: draftText
              anchors.centerIn: parent
              text: "📎 " + (root.draftLabel.length > 40
                              ? root.draftLabel.slice(0, 37) + "…"
                              : root.draftLabel) + "   ✕"
              textFormat: Text.PlainText
              color: root.mineText
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.caption)
            }
            HoverHandler { cursorShape: Qt.PointingHandCursor }
            TapHandler { onTapped: root.clearDraft() }
          }
          Item { Layout.fillWidth: true }
        }

        RowLayout {
          Layout.fillWidth: true
          visible: root.inThread
          spacing: root.space(6)

          TextField {
            id: composeField
            Layout.fillWidth: true
            // NEVER disabled: this field is the panel's exclusive keyboard-focus
            // holder, and disabling the focused editor dismisses the whole
            // panel (0.7.2 postmortem; Codex design review #8). readOnly
            // instead; send() is the authoritative online/sendability guard.
            enabled: true
            readOnly: !root.online || !root.isSendable(root.active)
            placeholderText: root.draftPath !== ""
              ? "caption (optional) — Enter sends the file"
              : root.isSendable(root.active) ? "iMessage" : "Read-only — group id unknown"
            foreground: root.foreground
            accent: root.mineFill
            font.family: root.fontFamily
            font.pixelSize: root.fontSize(Style.font.bodySmall)
            onAccepted: root.send()
            Keys.onEscapePressed: root.back()
            // Ctrl+V goes through paste.ts: an image on the clipboard becomes
            // a draft chip; text falls through to a manual insert. One process
            // snapshots types AND data — probing then re-reading races.
            Keys.onPressed: (event) => {
              if (event.matches(StandardKey.Paste)) {
                event.accepted = true
                root.startPaste()
              }
            }
          }

          // send button — the blue arrow circle (lit when text OR a file is queued)
          Rectangle {
            readonly property bool armed: composeField.text.trim() !== "" || root.draftPath !== ""
            width: root.space(28); height: width; radius: width / 2
            color: armed ? root.mineFill : Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.15)
            Text {
              anchors.centerIn: parent
              text: "↑"
              color: parent.armed ? "#ffffff" : root.dim
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.body)
              font.bold: true
            }
            TapHandler { onTapped: root.send() }
          }
        }

        Text {
          Layout.fillWidth: true
          visible: root.note !== ""
          text: root.note
          textFormat: Text.PlainText
          color: root.note === "sending…" ? root.dim : root.urgent
          font.family: root.fontFamily
          font.pixelSize: root.fontSize(Style.font.caption)
          wrapMode: Text.WordWrap
        }
      }
    }
  }

  component TapbackReaction: Rectangle {
    id: tapbackPill
    required property bool mine
    required property var reactions
    visible: reactions.length > 0
    z: 10
    readonly property real minimumSize: Math.max(
      root.space(32), Math.ceil(tapbackText.implicitHeight) + root.space(12)
    )
    width: Math.max(minimumSize, Math.ceil(tapbackText.implicitWidth) + root.space(18))
    height: minimumSize
    radius: height / 2
    // Composite once against the theme background so the pill and its tail
    // remain opaque where they overlap a translucent bubble or link card.
    color: root.incomingColorSetting === "theme"
      ? root.opaqueOver(
          Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.28),
          Color.background
        )
      : root.opaqueOver(Qt.darker(root.theirsFill, 1.2), Color.background)
    border.width: 0
    anchors.top: parent.top
    anchors.topMargin: -Math.round(height * 0.74)
    x: mine
      ? -Math.round(width * 0.35)
      : parent.width - width + Math.round(width * 0.35)

    Rectangle {
      id: tapbackTailLarge
      width: Math.round(tapbackPill.height * 0.28)
      height: width
      radius: width / 2
      color: tapbackPill.color
      x: tapbackPill.mine ? 0 : tapbackPill.width - width
      y: Math.round(tapbackPill.height * 0.76)
    }
    Rectangle {
      id: tapbackTailSmall
      width: Math.max(3, Math.round(tapbackPill.height * 0.14))
      height: width
      radius: width / 2
      color: tapbackPill.color
      x: tapbackPill.mine
        ? -Math.round(tapbackPill.height * 0.08)
        : tapbackPill.width - width + Math.round(tapbackPill.height * 0.08)
      y: Math.round(tapbackPill.height * 1.08)
    }
    Text {
      id: tapbackText
      z: 1
      anchors.centerIn: parent
      text: root.tapbackRow(tapbackPill.reactions)
      textFormat: Text.PlainText
      font.pixelSize: root.fontSize(Style.font.heading)
    }
  }

  // Incoming group runs share one avatar lane. Every message stays aligned,
  // while only the final visual row from a sender actually paints the photo.
  component GroupMessageAvatarSlot: Item {
    id: groupAvatarSlot
    required property bool reserve
    required property string handle
    required property string name
    required property bool showAvatar
    Layout.preferredWidth: reserve ? root.groupMessageAvatarSlot : 0
    Layout.preferredHeight: showAvatar ? root.groupMessageAvatarSize : 1

    function ensureAvatar() {
      if (showAvatar && handle !== "") root.requestAvatar(handle)
    }
    Component.onCompleted: ensureAvatar()
    onShowAvatarChanged: ensureAvatar()
    onHandleChanged: ensureAvatar()

    Rectangle {
      id: groupAvatarCircle
      visible: groupAvatarSlot.showAvatar
      anchors.left: parent.left
      anchors.bottom: parent.bottom
      width: root.groupMessageAvatarSize
      height: width
      radius: width / 2
      color: Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.18)

      Image {
        id: groupAvatarImage
        anchors.fill: parent
        visible: false
        source: root.avatarFiles[groupAvatarSlot.handle] || ""
        asynchronous: true
        fillMode: Image.PreserveAspectCrop
        sourceSize.width: 96
        sourceSize.height: 96
        onStatusChanged: if (status === Image.Error && groupAvatarSlot.handle !== "") {
          var files = Object.assign({}, root.avatarFiles)
          files[groupAvatarSlot.handle] = ""
          root.avatarFiles = files
        }
      }
      Item {
        id: groupAvatarMask
        anchors.fill: parent
        visible: false
        layer.enabled: true
        Rectangle { anchors.fill: parent; radius: width / 2 }
      }
      MultiEffect {
        anchors.fill: parent
        source: groupAvatarImage
        visible: groupAvatarImage.status === Image.Ready
        maskEnabled: true
        maskSource: groupAvatarMask
      }
      Text {
        anchors.centerIn: parent
        visible: groupAvatarImage.status !== Image.Ready
        text: {
          var n = String(groupAvatarSlot.name || groupAvatarSlot.handle || "")
          if (/^[+0-9]/.test(n) || n === "") return "#"
          var parts = n.trim().split(/\s+/)
          return (parts[0][0] + (parts.length > 1 ? parts[parts.length - 1][0] : "")).toUpperCase()
        }
        textFormat: Text.PlainText
        color: root.foreground
        font.family: root.fontFamily
        font.pixelSize: root.fontSize(Style.font.caption)
        font.bold: true
      }
    }
  }

  component PinnedConversation: Item {
    id: pinTile
    required property var thread
    property bool selected: false
    readonly property int pinAvatarSize: Math.max(
      Math.round(Style.spaceReal(root.avatarSize) * 1.75), root.space(48))
    readonly property string avatarHandle: String(thread.handle || thread.chat || "")
    readonly property string displayName: String(thread.pin_name || thread.name || thread.chat || "")

    Rectangle {
      anchors.fill: parent
      radius: root.corner(root.space(12))
      color: pinHover.hovered || pinTile.selected
        ? Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.08)
        : "transparent"
    }

    Column {
      anchors.fill: parent
      anchors.margins: root.space(4)
      spacing: root.space(4)

      Item {
        width: parent.width
        height: pinTile.pinAvatarSize

        Rectangle {
          id: pinAvatar
          anchors.horizontalCenter: parent.horizontalCenter
          width: pinTile.pinAvatarSize
          height: width
          radius: width / 2
          color: Qt.rgba(root.foreground.r, root.foreground.g, root.foreground.b, 0.18)
          Component.onCompleted: if (!root.isGroupId(String(pinTile.thread.chat || "")))
            root.requestAvatar(pinTile.avatarHandle)

          Image {
            id: pinAvatarImage
            anchors.fill: parent
            visible: false
            source: root.avatarFiles[pinTile.avatarHandle] || ""
            asynchronous: true
            fillMode: Image.PreserveAspectCrop
            sourceSize.width: 128
            sourceSize.height: 128
            onStatusChanged: if (status === Image.Error && pinTile.avatarHandle !== "") {
              var files = Object.assign({}, root.avatarFiles)
              files[pinTile.avatarHandle] = ""
              root.avatarFiles = files
            }
          }
          Item {
            id: pinAvatarMask
            anchors.fill: parent
            visible: false
            layer.enabled: true
            Rectangle { anchors.fill: parent; radius: width / 2 }
          }
          MultiEffect {
            anchors.fill: parent
            source: pinAvatarImage
            visible: pinAvatarImage.status === Image.Ready
            maskEnabled: true
            maskSource: pinAvatarMask
          }
          Text {
            anchors.centerIn: parent
            visible: pinAvatarImage.status !== Image.Ready
            text: {
              var name = pinTile.displayName
              if (/^[+0-9]/.test(name) || name === "") return "#"
              var parts = name.trim().split(/\s+/)
              return (parts[0][0] + (parts.length > 1 ? parts[parts.length - 1][0] : "")).toUpperCase()
            }
            textFormat: Text.PlainText
            color: root.foreground
            font.family: root.fontFamily
            font.pixelSize: root.fontSize(Style.font.bodySmall)
            font.bold: true
          }

          Rectangle {
            visible: Number(pinTile.thread.unread || 0) > 0
            anchors.right: parent.right
            anchors.top: parent.top
            anchors.rightMargin: -root.space(2)
            anchors.topMargin: -root.space(2)
            width: Math.max(root.space(18), unreadCount.implicitWidth + root.space(8))
            height: root.space(18)
            radius: height / 2
            color: root.mineFill
            border.width: 2
            border.color: Color.background
            Text {
              id: unreadCount
              anchors.centerIn: parent
              text: Number(pinTile.thread.unread || 0) > 99 ? "99+" : String(pinTile.thread.unread || "")
              textFormat: Text.PlainText
              color: root.mineText
              font.family: root.fontFamily
              font.pixelSize: root.fontSize(Style.font.caption)
              font.bold: true
            }
          }
        }
      }

      Text {
        width: parent.width
        text: pinTile.displayName
        textFormat: Text.PlainText
        color: root.foreground
        horizontalAlignment: Text.AlignHCenter
        elide: Text.ElideRight
        maximumLineCount: 1
        font.family: root.fontFamily
        font.pixelSize: root.fontSize(Style.font.caption)
        font.bold: Number(pinTile.thread.unread || 0) > 0
      }
    }

    HoverHandler { id: pinHover; cursorShape: Qt.PointingHandCursor }
    TapHandler { onTapped: root.openThread(pinTile.thread) }
    TapHandler {
      acceptedButtons: Qt.RightButton
      onTapped: root.openContactContext(pinTile.thread, "")
    }
  }

  // Qt's native Menu reserves rows for invisible children, so direct and
  // group conversations use separate menus instead of hiding irrelevant
  // actions. This avoids blank rows and disabled group flyouts in DMs.
  component ContactActionSubmenu: Menu {
    id: actionMenu
    required property bool copyAction
    title: copyAction ? "Copy vCard" : "Edit contact"
    width: root.space(360)
    Instantiator {
      model: root.contextPeople
      delegate: MenuItem {
        required property var modelData
        text: root.personMenuLabel(modelData)
        onTriggered: {
          if (actionMenu.copyAction) root.copyContactVCard(modelData)
          else root.editContact(modelData)
        }
      }
      onObjectAdded: function(index, object) { actionMenu.insertItem(index, object) }
      onObjectRemoved: function(index, object) { actionMenu.removeItem(object) }
    }
  }

  Menu {
    id: messageOnlyMenu
    width: root.space(300)
    MenuItem {
      text: "Copy message"
      onTriggered: root.copyText(root.contextMessageText)
    }
  }

  Menu {
    id: directContactMenu
    width: root.space(340)
    MenuItem {
      text: "Copy vCard — " + String(root.contextPeople[0] && root.contextPeople[0].name || "contact")
      onTriggered: root.copyContactVCard(root.contextPeople[0])
    }
    MenuItem {
      text: "Edit contact — " + String(root.contextPeople[0] && root.contextPeople[0].name || "contact")
      onTriggered: root.editContact(root.contextPeople[0])
    }
  }

  Menu {
    id: directMessageMenu
    width: root.space(340)
    MenuItem {
      text: "Copy message"
      onTriggered: root.copyText(root.contextMessageText)
    }
    MenuSeparator { }
    MenuItem {
      text: "Copy vCard — " + String(root.contextPeople[0] && root.contextPeople[0].name || "contact")
      onTriggered: root.copyContactVCard(root.contextPeople[0])
    }
    MenuItem {
      text: "Edit contact — " + String(root.contextPeople[0] && root.contextPeople[0].name || "contact")
      onTriggered: root.editContact(root.contextPeople[0])
    }
  }

  Menu {
    id: groupContactMenu
    width: root.space(300)
    ContactActionSubmenu { copyAction: true }
    ContactActionSubmenu { copyAction: false }
  }

  Menu {
    id: groupMessageMenu
    width: root.space(300)
    MenuItem {
      text: "Copy message"
      onTriggered: root.copyText(root.contextMessageText)
    }
    MenuSeparator { }
    ContactActionSubmenu { copyAction: true }
    ContactActionSubmenu { copyAction: false }
  }

  Timer {
    id: contactToastTimer
    interval: 3000
    onTriggered: root.contactToast = ""
  }

  Rectangle {
    z: 1000
    visible: root.contactToast !== "" && !root.settingsMode
    anchors.right: parent.right
    anchors.bottom: parent.bottom
    anchors.margins: root.space(14)
    implicitWidth: Math.min(root.space(480), contactToastText.implicitWidth + root.space(24))
    implicitHeight: contactToastText.implicitHeight + root.space(16)
    radius: root.corner(root.space(9))
    color: root.contactToastError
      ? Qt.rgba(root.urgent.r, root.urgent.g, root.urgent.b, 0.22)
      : Qt.rgba(root.accent.r, root.accent.g, root.accent.b, 0.22)
    border.width: 1
    border.color: root.contactToastError ? root.urgent : root.accent
    Text {
      id: contactToastText
      anchors.fill: parent
      anchors.margins: root.space(8)
      text: root.contactToast
      textFormat: Text.PlainText
      wrapMode: Text.WordWrap
      color: root.foreground
      font.family: root.fontFamily
      font.pixelSize: root.fontSize(Style.font.caption)
    }
  }

  BlipSettings {
    id: settingsView
    anchors.fill: parent
    visible: root.settingsMode
    preferences: root.preferences
    hostWidget: root.hostWidget
    threads: root.threads
    foreground: root.foreground
    urgent: root.urgent
    accent: root.accent
    outgoingFill: root.mineFill
    outgoingText: root.mineText
    incomingFill: root.theirsFill
    incomingText: root.theirsText
    fontFamily: root.fontFamily
    fontScale: root.fontScale
    density: root.density
    cornerScale: root.cornerScale
    onVcardFinished: function(message, success) {
      root.showContactToast(message, !success)
    }
    onCloseRequested: root.closeSettings()
  }

    // drag a file from a file manager onto the open conversation → draft chip
    DropArea {
      anchors.fill: parent
      enabled: root.inThread && !root.settingsMode
      keys: ["text/uri-list"]
      onDropped: (drop) => {
        if (!drop.hasUrls || drop.urls.length === 0) return
        var u = String(drop.urls[0])
        if (u.indexOf("file://") !== 0) return
        root.setDraft(decodeURIComponent(u.replace(/^file:\/\//, "")))
        drop.accept()
      }
    }
}
